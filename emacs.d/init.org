#+title: Emacs Init File!
#+property: header-args :results silent :comments link :tangle ~/dotfiles/emacs.d/init.el

* Intro

This is the bulk of my emacs configuration, which gets tangled to [[./init.el][init.el]]. It's
pretty cool that I can write it in org mode, because easy organization and
rearrangement are sweet.

* Contents                                                   :toc_1:noexport:
- [[#intro][Intro]]
- [[#initial-setup][Initial Setup]]
- [[#core][Core]]
- [[#help][Help]]
- [[#filesbuffers][Files/Buffers]]
- [[#editing][Editing]]
- [[#navigation][Navigation]]
- [[#appearance][Appearance]]
- [[#dev][Dev]]
- [[#tools][Tools]]
- [[#emacs-enhancements][Emacs Enhancements]]
- [[#fun][Fun]]
- [[#web-services][Web services]]
- [[#local-vars][Local vars]]

* Initial Setup
** Details
*** Lexical binding
In accordance with the [[https://github.com/bbatsov/emacs-lisp-style-guide#source-code-layout--organization][emacs-lisp-style-guide]].
#+begin_src emacs-lisp :comments no :cond-case no :padline no
;;; -*- lexical-binding: t -*-
#+end_src
*** package-initialize
This is required (even though I don't use package.el) because ~package.el~ is
annoying like that.
#+begin_src emacs-lisp :comments no :cond-case no :padline no
;;(package-initialize)
#+end_src
** Startup time
Next we define a variable for timing startup, and change ~gc-cons-threshhold~
temporarily (only during startup). This saves me like .3 s of startup time.
#+begin_src emacs-lisp
(defvar d/file-name-handler-alist file-name-handler-alist)
(setq gc-cons-threshold 400000000
      gc-cons-percentage 0.6
      file-name-handler-alist nil
      ;; sidesteps a bug when profiling with esup
      esup-child-profile-require-level 0)
#+end_src
Make sure to reset the variables after init.
#+begin_src emacs-lisp
(add-hook 'after-init-hook
          `(lambda ()
             (setq gc-cons-threshold 800000
                   gc-cons-percentage 0.1
                   file-name-handler-alist d/file-name-handler-alist)))
#+end_src
** Custom
Custom.el and the a custom file can be a nice convenience at first, but over
time stuff kind of just accumulates to the point where you don't even know
where anything came from anymore, and it's hard to track through version
control.
#+begin_src emacs-lisp
(setq custom-file (expand-file-name "custom.el" temporary-file-directory))
#+end_src
** Load Path
Add [[./lisp][lisp]] directory and subdirectories to ~load-path~ and ~custom-theme-load-path~.

This is where I put lisp that isn't necessarily central to my config or needs
to be in a standalone file such as a some auth settings, lisp practice,
really bad self-made themes, ~org-export-async-init-file~, etc.
#+begin_src emacs-lisp
(eval-and-compile
  (add-to-list 'load-path (expand-file-name "lisp" user-emacs-directory)))
#+end_src
** Libraries
~cl-lib~ gives us a wealth of common-lisp inspired or based functions and
macros that just make our lives easier. ~subr-x~ is similar. I'm sure these
will be required by some package somewhere along the way, but I use them a fair
bit so let's explicitly require them.
#+begin_src emacs-lisp
(require 'cl-lib)
(require 'subr-x)
#+end_src
** pass
#+begin_src emacs-lisp
;; (auth-source-pass-enable)
#+end_src
** Init utilities
It can be annoying to have a bunch of ~add-hook~ statements, some with lambdas.
This just makes a hook function with the given body and adds that.
#+begin_src emacs-lisp
(defmacro d/hook! (hooks &rest body)
  "Create a setup function for HOOKS and add it to relevant hook(s)."
  (declare (indent defun))
  (let ((hooks (if (listp hooks) hooks (list hooks))))
    (let ((setup-func (intern (concat "d/setup-" (symbol-name (car hooks))))))
      `(progn
         (defun ,setup-func ()
           ,@body)
         ,@(cl-loop for hook in hooks collect
                    (let ((hook-name (intern (concat (symbol-name hook)
                                                     "-hook"))))
                      `(add-hook ',hook-name #',setup-func)))))))
#+end_src
~use-package~ declarations avoid major breakage when something is configured
incorrectly by wrapping code in ~condition-case-unless-debug~. I wanted a
version of ~with-eval-after-load~ that can do that, so I can split up package
configuration into different src blocks for organization, without giving up
error handling and lazy loading.
#+begin_src emacs-lisp
(defmacro d/after (feature &rest body)
  "Load BODY after FEATURE, catching errors and displaying as warnings."
  (declare (indent defun))
  `(with-eval-after-load ,feature
     (condition-case-unless-debug err
         (progn
           ,@body)
       (error
        (display-warning
         'init
         (format "%s eval-after-load: %s "
                 (symbol-name ,feature)
                 (error-message-string err))
         :error)))))
#+end_src
Like ~add-to-list~, but accepts multiple.
#+begin_src emacs-lisp
(defun d/list-prepend! (list &rest args)
  "Prepend each of ARGS to LIST (a symbol).

Args are prepended in reverse order, so the first arg will be the
first element of the list. Convenience wrapper around
`add-to-list'."
  (declare (indent defun))
  (cl-loop for thing in (reverse args)
           do (add-to-list list thing)))
#+end_src
Just a wrapper around ~safe-local-variable-values~, allowing for multiple
safe variable declarations.
#+begin_src emacs-lisp
(defmacro d/safe-vars! (&rest vars)
  "Add VARS to `safe-local-variable-values'."
  (declare (indent defun))
  (cl-loop for thing in vars
           as thing = (if (listp thing) thing (list thing))
           do (add-to-list 'safe-local-variable-values thing)))
#+end_src
** Package management
*** [[https://github.com/raxod502/straight.el][straight]]
#+begin_quote
Next-generation, purely functional package manager for the Emacs hacker.
#+end_quote
This gives me finer control over my packages - I can specify a gitsha to pin
packages to, as well as "freeze" packages for a fully reproducible install. The
only packages that are persistently loaded across sessions are those declared
in the init.

Also, to patch, edit, or contribute to a package, I can simply ~M-x
find-library~ and edit/push/pull request away, since package sources are stored
as git repos.
#+begin_src emacs-lisp
(setq straight-repository-branch "develop")

(let ((bootstrap-file (locate-user-emacs-file "straight/bootstrap.el"))
      (bootstrap-version 3))
  (unless (file-exists-p bootstrap-file)
    (with-current-buffer
        (url-retrieve-synchronously
         "https://raw.githubusercontent.com/raxod502/straight.el/develop/install.el"
         'silent 'inhibit-cookies)
      (goto-char (point-max))
      (eval-print-last-sexp)))
  (load bootstrap-file nil 'nomessage))

(setq straight-process-buffer " *straight-process*"
      straight-cache-autoloads t
      straight-use-package-by-default t
      straight-vc-git-default-protocol (if (getenv "TERMUX") 'https 'ssh)
      straight-vc-git-force-protocol t)

(add-hook 'after-init-hook
          (defun d/straight-prune-if-success ()
            (run-with-idle-timer
             15 nil
             (lambda ()
               (when (not init-file-had-error)
                 (straight-prune-build))))))
#+end_src
*** [[https://github.com/jwiegley/use-package][use-package]]
#+begin_quote
A use-package declaration for simplifying your .emacs
#+end_quote
An excellent utility for managing packages and package configuration in a neat
and organized way, with advanced support for deferring, pre/post-loading
configuration, time reporting, and more.

With use-package, you can use the same init file across computers without
keeping track of what's installed or not and it will ensure that any missing
packages are installed. It's pretty neat.
#+begin_src emacs-lisp
(straight-use-package 'use-package)
(setq use-package-minimum-reported-time .001
      use-package-verbose t
      use-package-always-defer t
      use-package-compute-statistics t)
#+end_src
** Packages
*** [[https://github.com/tarsius/no-littering/][no-littering]]
#+begin_quote
Help keeping ~/.emacs.d clean
#+end_quote
Usually, a bunch of crap is kept in your ~.emacs.d~ folder by both built-in emacs
features and external packages. This package sets up a convention to store
everything in either ~.emacs.d/var~ or ~.emacs.d/etc~.
#+begin_src emacs-lisp
(use-package no-littering
  :demand t
  :config
  (setq auto-save-file-name-transforms
        `((".*" ,(no-littering-expand-var-file-name "auto-save/") t))))
#+end_src
*** [[https://github.com/purcell/exec-path-from-shell][exec-path-from-shell]]
#+begin_quote
Make Emacs use the $PATH set up by the user's shell
#+end_quote
If you've ever had issues where emacs doesn't find your executables, this
package should fix them nicely.
#+begin_src emacs-lisp :tangle no
(use-package exec-path-from-shell
  :defer 5
  :config
  (setq exec-path-from-shell-check-startup-files nil)
  (exec-path-from-shell-initialize))
#+end_src
*** [[https://github.com/abo-abo/hydra][hydra]]
#+begin_quote
make Emacs bindings that stick around
#+end_quote
#+begin_src emacs-lisp
(use-package hydra
  :config
  (setq hydra-default-hint nil))
#+end_src
*** [[elisp:(find-library-other-window%20"server")][server]]
Start server if not already running. You can do this with ~emacs --daemon~,
automate it with systemd, or use ~brew services start emacs~ on macOS. I
usually just run Emacs on login anyway, so this suffices.

This makes startup time irrelevant. Start emacs once, connect with emacsclient
every other time. See [[https://www.gnu.org/software/emacs/manual/html_node/emacs/Emacs-Server.html#Emacs-Server][Using Emacs as a Server]].
#+begin_src emacs-lisp
(use-package server
  :defer 3
  :config
  (unless (server-running-p)
    (server-start)))
#+end_src
*** [[elisp:(find-library-other-window%20"savehist")][savehist]]
#+begin_src emacs-lisp
(use-package savehist
  :defer 5
  :config
  (savehist-mode 1)
  (add-to-list 'savehist-additional-variables 'kill-ring))
#+end_src
*** [[elisp:(find-library-other-window%20"saveplace")][saveplace]]
#+begin_src emacs-lisp
(use-package saveplace
  :defer 5
  :config
  (save-place-mode 1))
#+end_src
*** [[elisp:(find-library-other-window%20"midnight")][midnight-mode]]
#+begin_src emacs-lisp
(use-package midnight
  :defer 10
  :config
  (setq clean-buffer-list-delay-general 1
        clean-buffer-list-kill-regexps '("\\`\\*Man " "\\`\\*helpful "))
  (midnight-mode))
#+end_src
* Core
** Defaults
*** Disabled Commands
By default, some emacs commands are disabled because I guess they're considered
"confusing" to beginners or something. Nonsense! :)
#+begin_src emacs-lisp
(setq disabled-command-function nil)
#+end_src
*** Local vars
Only enable local variables that have been added to
~safe-local-variable-values~. I mostly just don't like being prompted to enable
local vars willy nilly, and usually add something to safe local vars if I
really need it.
#+begin_src emacs-lisp
(setq enable-local-variables :safe)
#+end_src
*** Kill-ring
Save stuff you've copied in other applications to the emacs kill-ring.
#+begin_src emacs-lisp
(setq save-interprogram-paste-before-kill t)
#+end_src
*** Messages
Allow more messages in ~*Messages*~ buffer so you can look at what happened waaay
back if you need to.
#+begin_src emacs-lisp
(setq message-log-max 10000)
#+end_src
*** Minibuffer
Allow editing in the minibuffer... /with/ the minibuffer.
#+begin_src emacs-lisp
(setq enable-recursive-minibuffers t
      resize-mini-windows t)
#+end_src
*** Prompts
Having to type "yes" can be annoying.
#+begin_src emacs-lisp
(defalias 'yes-or-no-p #'y-or-n-p)
#+end_src
*** clipboard
#+begin_src emacs-lisp
(setq x-select-enable-primary t
      mouse-yank-at-point t)
#+end_src
*** Scratch
There's kind of no point in using a whole separate mode for the scratch buffer.
Setting to emacs-lisp-mode reduces the amount of configuration we have to do.
Here, I set it to fundamental-mode so as to avoid unnecessary prog-mode or
emacs-lisp-mode hooks being called on startup, and load emacs-lisp-mode with a
delay in the config for persistent-scratch.
#+begin_src emacs-lisp
(setq initial-scratch-message ""
      initial-major-mode 'fundamental-mode)
#+end_src
*** Tab
Use tab for completion in region and cycle candidates.
#+begin_src emacs-lisp
(setq tab-stop-list (number-sequence 4 200 4)
      completion-cycle-threshold t
      tab-always-indent 'complete)
#+end_src
*** Time Display
#+begin_src emacs-lisp :tangle no
(d/after 'time
  (setq display-time-24hr-format t
        display-time-format "%Y-%d-%m %H:%M "
        display-time-default-load-average nil
        display-time-load-average nil))
#+end_src
*** Tramp
Use ssh by default and remember passwords for [[https://www.gnu.org/software/emacs/manual/html_node/tramp/index.html][tramp]]. Also make tramp quieter
except for warnings and errors.
#+begin_src emacs-lisp
(setq tramp-default-method "ssh"
      tramp-verbose 2)
#+end_src
*** Passwords
#+begin_src emacs-lisp
(setq password-cache t
      password-cache-expiry 86400)
#+end_src
*** Authinfo
#+begin_src emacs-lisp
(setq auth-sources '("~/.authinfo.gpg"))
#+end_src
*** EPA
Integrate gpg passphrase query with Emacs minibuffer when using EPA.
#+begin_src emacs-lisp
(setq epa-pinentry-mode 'loopback)
#+end_src
** Functions
*** Dotfiles
Here I define a function that uses [[https://github.com/jwiegley/emacs-async][async.el]] to open and tangle an org file
asynchronously. This is how I tangle init.org (and rc.org) directly to init.el
on save, since it's long enough that a synchronous tangle causes a pretty
annoynig lag.

- If ~d/asyncbabel-tangle-decrypt~ is set to ~t~ in the [[https://www.gnu.org/software/emacs/manual/html_node/emacs/Specifying-File-Variables.html#Specifying-File-Variables][file variables]], org entries
  are decrypted first. Mostly useful if you do something like keep sensitive
  configuration files in an org file.
- If ~d/show-async-tangle-results~ is ~t~, don't dispose of async emacs process
  buffers. This is useful for debugging errors.
#+begin_src emacs-lisp
(d/after 'org
  (defvar d/show-async-tangle-results nil
    "Keep *emacs* async buffers around for later inspection.")
  (defvar d/async-babel-tangle-decrypt nil
    "Decrypt org entries before tangling.

Probably most useful as a file-local variable.")

  (defun d/async-babel-tangle (&optional decrypt)
    "Tangle org file asynchronously.

If optional DECRYPT argument is given, dercypt entries before
tangling."
    (interactive)
    (let ((init-tangle-start-time (current-time))
          (file (buffer-file-name))
          (async-quiet-switch "-q"))
      (async-start
       `(lambda ()
          (require 'org)
          (when ,d/async-babel-tangle-decrypt
            (require 'org-crypt)
            (org-crypt-use-before-save-magic)
            (add-hook 'org-babel-pre-tangle-hook 'org-decrypt-entries)
            (remove-hook 'org-babel-pre-tangle-hook 'save-buffer))
          (org-babel-tangle-file ,file))
       (unless d/show-async-tangle-results
         `(lambda (result)
            (if result
                (message "SUCCESS: init.org successfully tangled. (%.3fs)"
                         (float-time (time-subtract (current-time)
                                                    ',init-tangle-start-time)))
              (message "ERROR: init.org tangle failed."))))))))
#+end_src
Declare adding d/async-babel-tangle to after-save-hook locally a safe eval form
so emacs doesn't ask every time.
#+begin_src emacs-lisp
(d/after 'org
  (d/safe-vars!
    (eval add-hook 'after-save-hook 'd/async-babel-tangle 'append 'local)))
#+end_src
** Packages
These are packages that I consider /absolutely essential/ to my emacs workflow,
or that enhance emacs at a deeper level than any regular mode.
*** [[https://github.com/noctuid/general.el][general]]
#+begin_quote
More convenient key definitions in emacs
#+end_quote
That undersells it. The /most/ convenient key definitions in emacs.
#+begin_src emacs-lisp
(use-package general
  :demand t
  :config
  (general-evil-setup t)

  (general-override-mode)

  (general-create-definer
    d/mode-leader-keys
    :keymaps 'override
    :states '(emacs normal visual motion insert)
    :non-normal-prefix "C-,"
    :prefix ",")

  (general-create-definer
    d/leader-keys
    :keymaps 'override
    :states '(emacs normal visual motion insert)
    :non-normal-prefix "C-SPC"
    :prefix "SPC"))
#+end_src
*** [[https://github.com/emacs-evil/evil][evil]]
#+begin_quote
The extensible vi layer for Emacs.
#+end_quote
I really like Vim bindings. I originally learned Emacs bindings but there was
something really appealing about the simplicity and power of modal editing. So
I went for it. Now I'll never go back.
**** package
#+begin_src emacs-lisp
(use-package evil
  :demand t
  :general
  (mmap
    "-" 'negative-argument
    ;; Basically C-[ for a Dvorak keyboard (_ is for terminal).
    "C-_" 'keyboard-quit
    "C-/"  'keyboard-quit
    [escape]  'keyboard-quit)
  (:states '(insert replace visual)
   "C-_" 'evil-normal-state
   "C-/" 'evil-normal-state)
  (vmap [escape] 'keyboard-quit)
  :custom
  (evil-want-C-u-scroll t)
  (evil-want-integration nil)
  (evil-want-fine-undo t)
  (evil-search-module 'evil-search)
  (evil-lookup-func (lambda () (man (thing-at-point 'word))))
  :config
  (setq evil-insert-state-cursor '(bar . 1) ;thinner cursors
        evil-emacs-state-cursor '(bar . 1)
        evil-ex-search-vim-style-regexp t
        evil-split-window-below t
        evil-vsplit-window-right t
        ;; This was more relevant when I displayed the tag in the mode-line.
        evil-normal-state-tag  " N "
        evil-insert-state-tag  " I "
        evil-motion-state-tag  " M "
        evil-visual-state-tag  " V "
        evil-emacs-state-tag   " E "
        evil-replace-state-tag " R "
        evil-operator-state-tag " O ")

  (evil-define-command evil-view (file &optional bang)
    "Open FILE in view-mode.
If no FILE is specified, change current buffer to view mode."
    :repeat nil
    (interactive "<f>")
    (cond (file
           (view-file file))
          (view-mode
           (view-mode -1))
          (t (view-mode))))

  (evil-ex-define-cmd "dtw" #'delete-trailing-whitespace)

  (evil-mode 1))
#+end_src
**** support
***** [[https://github.com/syohex/emacs-evil-anzu][evil-anzu]]
Show evil search current/total count in the mode line.
#+begin_src emacs-lisp
(use-package evil-anzu
  :defer 15
  :config
  (setq anzu-cons-mode-line-p nil)
  (defun d/anzu-update-mode-line (here total)
    (when anzu--state
      (let ((status (cl-case anzu--state
                      (search (format "%s/%d%s"
                                      (anzu--format-here-position here total)
                                      total (if anzu--overflow-p "+" "")))
                      (replace-query (format "(%d replace)" total))
                      (replace (format "(%d/%d)" here total)))))
        status)))
  (setq anzu-mode-line-update-function #'d/anzu-update-mode-line))
#+end_src
***** [[https://github.com/jojojames/evil-collection][evil-collection]]
#+begin_quote
 A set of keybindings for evil-mode
#+end_quote
Evil integration for a bunch of misc modes.
#+begin_src emacs-lisp
(use-package evil-collection
  :custom
  (evil-collection-company-use-tng nil)
  (evil-collection-setup-minibuffer t)
  (evil-collection-term-sync-state-and-mode-p t)
  :init
  (evil-collection-init))
#+end_src
***** [[https://github.com/cute-jumper/evil-embrace.el][evil-embrace]]
Additional support for custom surrounding pairs for evil-surround.
#+begin_src emacs-lisp
(use-package evil-embrace
  :after evil-surround
  :demand t
  :hook (org-mode . embrace-org-mode-hook)
  :custom
  (embrace-show-help-p nil)
  :config
  (evil-embrace-enable-evil-surround-integration))
#+end_src
***** [[https://github.com/TheBB/evil-indent-plus][evil-indent-plus]]
#+begin_quote
Better indent textobjects for evil
#+end_quote
#+begin_src emacs-lisp
(use-package evil-indent-plus
  :general
  (itomap
    "i" 'evil-indent-plus-i-indent
    "I" 'evil-indent-plus-i-indent-up
    "J" 'evil-indent-plus-i-indent-up-down)
  (otomap
    "i" 'evil-indent-plus-a-indent
    "I" 'evil-indent-plus-a-indent-up
    "J" 'evil-indent-plus-a-indent-up-down))
#+end_src
***** [[https://github.com/edkolev/evil-lion][evil-lion]]
#+begin_src emacs-lisp
(use-package evil-lion
  :general
  (nvmap
    "gl" 'evil-lion-left
    "gL" 'evil-lion-right))
#+end_src
***** [[https://github.com/emacs-evil/evil-magit][evil-magit]]
#+begin_quote
Black magic or evil keys for magit
#+end_quote
#+begin_src emacs-lisp
(use-package evil-magit
  :after magit
  :demand t)
#+end_src
***** [[https://github.com/redguardtoo/evil-matchit][evil-matchit]]
A more generic ~%~ in evil.
#+begin_src emacs-lisp
(use-package evil-matchit
  :general
  (itomap "%" 'evilmi-inner-text-object)
  (otomap "%" 'evilmi-outer-text-object)
  (nvmap "%" 'evilmi-jump-items)
  :config (global-evil-matchit-mode 1))
#+end_src
***** [[https://github.com/redguardtoo/evil-nerd-commenter][evil-nerd-commenter]]
#+begin_quote
Comment/uncomment lines efficiently. Like Nerd Commenter in Vim
#+end_quote
#+begin_src emacs-lisp
(use-package evil-nerd-commenter
  :general
  (nmap
    "gc" 'evilnc-comment-operator
    "gy" 'evilnc-copy-and-comment-lines)
  (d/leader-keys
    "c"  '(:ignore t :wk "comment")
    "ci" 'd/comment-or-uncomment-lines-inverse
    "cl" 'evilnc-comment-or-uncomment-lines
    "cp" 'evilnc-comment-or-uncomment-paragraphs
    "ct" 'evilnc-comment-or-uncomment-to-the-line
    "cy" 'evilnc-copy-and-comment-lines)
  :config
  (defun d/comment-or-uncomment-lines-inverse (&optional arg)
    "Source: https://git.io/vQKza"
    (interactive "p")
    (let ((evilnc-invert-comment-line-by-line t))
      (evilnc-comment-or-uncomment-lines arg))))
#+end_src
***** [[https://github.com/dieggsy/evil-numbers][evil-numbers]]
#+begin_quote
Increment and decrement numbers in Emacs
#+end_quote
#+begin_src emacs-lisp
(use-package evil-numbers
  :straight (:host github
             :repo "dieggsy/evil-numbers"
             :fork (:host github
                    :repo "cofi/evil-numbers"))
  :general
  (d/leader-keys
    "n-"  'd/numbers/evil-numbers/dec-at-pt
    "n="  'd/numbers/evil-numbers/inc-at-pt)
  :config
  (defhydra d/numbers ()
    "
╭─────────╮
│ numbers │
└─────────┘
  [_=_] inc
  [_-_] dec
───────────
"
    ("="  evil-numbers/inc-at-pt)
    ("-" evil-numbers/dec-at-pt)))
#+end_src
***** [[https://github.com/Somelauw/evil-org-mode][evil-org]]
#+begin_quote
Supplemental evil-mode keybindings to emacs org-mode
#+end_quote
#+begin_src emacs-lisp
(use-package evil-org
  :hook (org-mode . evil-org-mode)
  :general
  (nvmap evil-org-mode-map
    "TAB" 'org-cycle
    "S-TAB" 'org-cycle)
  :config
  (evil-org-set-key-theme)
  (require 'evil-org-agenda)
  (evil-org-agenda-set-keys)
  (nmap evil-org-mode-map
    [backtab] 'org-shifttab)
  (d/after 'org-src
    (define-key org-src-mode-map [remap evil-write] 'org-edit-src-save)
    (define-key org-src-mode-map [remap evil-save-and-close]
      (lambda () (interactive)
        (org-edit-src-save)
        (org-edit-src-exit)))
    (define-key org-src-mode-map [remap evil-save-modified-and-close]
      (lambda () (interactive)
        (org-edit-src-save)
        (org-edit-src-exit)))))
#+end_src
***** [[https://github.com/emacs-evil/evil-surround][evil-surround]]
Helps work with surrounding chars better.
#+begin_src emacs-lisp
(use-package evil-surround
  :general
  (omap
    "s" 'evil-surround-edit
    "S" 'evil-Surround-edit)
  (vmap
    "S" 'evil-surround-region
    "gS" 'evil-Surround-region))
#+end_src
*** [[https://github.com/abo-abo/swiper][ivy]]
#+begin_quote
Ivy - a generic completion frontend for Emacs, Swiper - isearch with an
  overview, and more. Oh, man!
#+end_quote
A really nice search/completion system for emacs.
**** ivy
***** ivy
#+begin_src emacs-lisp
(use-package ivy
  :general
  (ivy-minibuffer-map
   [escape] 'keyboard-escape-quit
   "C-/" 'keyboard-escape-quit
   [S-return] 'ivy-dispatching-done-hydra
   [C-return] 'ivy-immediate-done
   "C-j" 'ivy-next-line
   "C-k" 'ivy-previous-line
   [S-up] 'ivy-previous-history-element
   [S-down] 'ivy-next-history-element
   "DEL" 'ivy-backward-delete-char)
  (d/leader-keys
    "-" 'ivy-resume
    "bb" 'ivy-switch-buffer
    "bB" 'ivy-switch-buffer-other-window)
  :config
  (ivy-mode 1)

  (defvar d/ivy-buffer-ignored-modes '(magit-mode erc-mode dired-mode grep-mode))

  (defun d/ivy-ignore-buffer? (str)
    "Return non-nil if str names a buffer with a major mode
derived from one of `d/ivy-buffer-ignored-modes'.

This function is intended for use with `ivy-ignore-buffers'."
    (let* ((buf (get-buffer str))
           (mode (and buf (buffer-local-value 'major-mode buf))))
      (and mode
           (apply #'provided-mode-derived-p mode d/ivy-buffer-ignored-modes))))

  (d/list-prepend! 'ivy-ignore-buffers
    'd/ivy-ignore-buffer?
    (rx "*"
        (or "LV" "epc" "Calc" "Colors" "help" "Packages" "Customize" "info"
            "Compile" "anaconda-mode" "scratch" "Messages" "elfeed-log" "Man"
            "Quail" "Paradox Report" "Backtrace" "slime-events"
            "slime-compilation" "inferior-lisp" "Completions" "embrace-help"
            "geiser messages*" "Geiser dbg" "tramp/" "Process List"
            "eshell:" "Error")))

  (defun d/ivy-dired-transformer (str)
    "Display transformer for dired buffers in `ivy-switch-buffer'.

Adds 'Dired: ' before dired buffer names.

Dired buffers are often just named after their directory, and
it's not always clear thet it's a dired buffer in ivy. Color
matching can mitigate this, but for short directory names the
color is hidden by ivy's match color."
    (let ((buf (get-buffer str))
          (str (ivy-switch-buffer-transformer str)))
      (if (and buf (eq (buffer-local-value 'major-mode buf) 'dired-mode))
          (concat (propertize "Dired: " 'face (get-text-property 0 'face str))
                  str)
        str)))

  (ivy-set-display-transformer 'ivy-switch-buffer 'd/ivy-dired-transformer)

  (setq ivy-re-builders-alist '((swiper . ivy--regex-plus)
                                (t . ivy--regex-ignore-order)))

  (d/list-prepend! 'ivy-sort-functions-alist
    `(ivy-completion-in-region
      . ,(lambda (str1 str2)
           (> 0 (compare-strings str1 nil nil str2 nil nil 'ignore-case)))))

  (setq ivy-format-function 'ivy-format-function-line
        ivy-use-virtual-buffers t
        ivy-count-format ""
        ivy-extra-directories nil
        ivy-use-selectable-prompt t
        ivy-display-functions-alist nil
        ivy-switch-buffer-faces-alist '((dired-mode . ivy-subdir)
                                        (wdired-mode . ivy-subdir)
                                        (ranger-mode . ivy-subdir))))
#+end_src
***** ivy-hydra
#+begin_src emacs-lisp
(use-package ivy-hydra
  :after ivy
  :demand t)
#+end_src
**** swiper
#+begin_src emacs-lisp
(use-package swiper
  :general
  (d/leader-keys
    "sm" 'swiper-multi
    "sS" 'swiper-all)
  :config (setq swiper-goto-start-of-match t))
#+end_src
**** counsel
#+begin_src emacs-lisp
(use-package counsel
  :general
  ("M-x" 'counsel-M-x
   "C-x C-f" 'counsel-find-file)
  (imap minibuffer-local-map
    "C-r" 'counsel-minibuffer-history)
  (d/leader-keys
    "SPC" 'counsel-M-x
    "aa"  'counsel-linux-app
    "ff"  'counsel-find-file
    "fF"  'find-file-other-window
    "fj"  'counsel-file-jump
    "fl"  'counsel-locate
    "hdF" 'counsel-describe-face
    "hdb" 'counsel-descbinds
    "hdf" 'counsel-describe-function
    "hdv" 'counsel-describe-variable
    "iu"  'counsel-unicode-char
    "sr"  'counsel-rg
    "ss"  'counsel-grep-or-swiper
    "y"   'counsel-yank-pop)
  :custom
  (counsel-find-file-ignore-regexp "\\`\\.")
  (counsel-yank-pop-preselect-last t)
  (counsel-describe-function-function #'helpful-callable)
  (counsel-describe-variable-function #'helpful-variable)
  (counsel-linux-apps-directories
   '("~/.local/share/applications/"
     "~/.guix-profile/share/applications/"
     "~/.nix-profile/share/applications/"
     "/usr/local/share/applications/"
     "/usr/share/applications/"))
  :config
  (when (eq system-type 'darwin)
    (setq counsel-locate-cmd 'counsel-locate-cmd-mdfind))

  (setq conusel-org-goto-display-style 'path
        counsel-org-headline-path-separator ": "
        counsel-org-goto-face-style 'org
        counsel-org-headline-display-todo t
        counsel-grep-base-command "rg -i -M 120 --no-heading --line-number --color never %s %s"
        counsel-yank-pop-separator "\n─────────────────────────\n"
        counsel-find-file-ignore-regexp (rx (or (group string-start (char ".#"))
                                                (group (char "~#") string-end)
                                                (group ".elc" string-end)
                                                (group ".pyc" string-end))))
  (counsel-mode 1)
  (defalias 'locate #'counsel-locate)

  (ivy-add-actions
   'counsel-find-file
   '(("e"
      (lambda (f)
        (let ((default-directory (if (file-directory-p f)
                                     f
                                   (file-name-directory f))))
          (d/eshell-here)))
      "eshell"))))
#+end_src
*** [[http://orgmode.org/][org-mode]]
#+begin_quote
Org mode is for keeping notes, maintaining TODO lists, planning projects, and
authoring documents with a fast and effective plain-text system.
#+end_quote
But really, it's life.
**** Package
#+begin_src emacs-lisp
(use-package org-plus-contrib
  :general
  (nmap org-mode-map
    "gt" 'org-todo
    "ga" 'org-archive-subtree)
  (mmap org-mode-map
    "RET" (general-predicate-dispatch nil
            (d/org-at-openable-item?) 'org-open-at-point
            (org-at-item-checkbox-p) 'org-toggle-checkbox
            (org-in-src-block-p) 'org-babel-execute-src-block))
  (d/leader-keys
    "C"   'org-capture
    "bo"  'org-iswitchb
    "ao"  '(:ignore t :wk "org")
    "ao#" 'org-agenda-list-stuck-projects
    "ao/" 'org-occur-in-agenda-files
    "aoO" 'org-clock-out
    "aoa" 'org-agenda-list
    "aoe" 'org-store-agenda-views
    "aol" 'org-store-link
    "aom" 'org-store-tags-view
    "aoo" 'org-agenda
    "aos" 'org-search-view
    "aot" 'org-todo-list )
  :custom
  (org-list-allow-alphabetical t)
  :init
  (d/safe-vars! (org-log-done))
  :config
  (d/after 'ox
    (require 'ox-extra)
    (ox-extras-activate '(ignore-headlines)))
  (d/after 'org-crypt
    (org-crypt-use-before-save-magic))
  (require 'org-mobile))
#+end_src
**** Defaults
***** Files
#+begin_src emacs-lisp
(d/after 'org
  (setq org-agenda-text-search-extra-files '(agenda-archives)
        org-agenda-files '("~/org/todo.org" "~/org/gcal.org")
        org-default-notes-file "~/org/todo.org"
        d/notes-file "~/org/notes.org"
        org-directory "~/org"
        org-archive-location "~/org/archive.org::"
        org-mobile-inbox-for-pull "~/org/mobile.org"
        org-export-async-init-file
        (locate-user-emacs-file "lisp/org-async-init.el")))
#+end_src
***** Todo/agenda
#+begin_src emacs-lisp
(d/after 'org
  (setq org-enforce-todo-dependencies t
        org-enforce-todo-checkbox-dependencies t
        org-log-done 'time
        org-log-redeadline 'time
        org-log-reschedule 'time
        org-agenda-skip-scheduled-if-done t
        org-agenda-skip-deadline-if-done t
        org-agenda-hide-tags-regexp ".*"
        org-agenda-span 'week)

  (setq org-agenda-deadline-faces
        '((1.0 . org-warning)
          (0.5 . org-upcoming-deadline)
          (0.0 . '(:foreground "#A89984"))))

  (setq org-todo-keywords
        '((sequence "TODO(t)" "IN-PROGRESS(p)" "WAITING(w)" "|"
                    "DONE(d)" "CANCELED(c)")
          (sequence "READ(r)" "|"
                    "DONE(h)")))

  (setq org-capture-templates
        '(("t" "Todo")
          ("ts" "Todo: School")
          ("te" "Todo: Emacs" entry
           (file+olp org-default-notes-file "Emacs")
           "* TODO %?")
          ("n" "Note")
          ("g" "Google calendar" entry
           (file "~/syncthing/org/gcal.org") "* %?\n\n%^T"))))
#+end_src
***** Behavior
#+begin_src emacs-lisp
(d/after 'org
  (setq org-confirm-babel-evaluate nil
        org-startup-indented t
        org-catch-invisible-edits 'error
        org-insert-heading-respect-content t
        org-src-window-setup 'current-window
        org-list-demote-modify-bullet '(("-" . "+") ("+" . "*") ("*" . "-"))
        org-export-in-background t
        org-confirm-babel-evaluate nil
        org-src-tab-acts-natively t
        org-M-RET-may-split-line nil
        org-list-use-circular-motion t
        org-log-into-drawer t
        org-imenu-depth 5
        org-goto-interface 'outline-path-completion
        org-outline-path-complete-in-steps nil
        org-link-search-must-match-exact-headline nil
        org-confirm-elisp-link-function 'y-or-n-p
        org-tags-exclude-from-inheritance '("crypt")
        org-confirm-elisp-link-not-regexp (rx "("
                                              (or "org-wiki-search"
                                                  "describe-function"
                                                  "describe-variable"
                                                  "find-library-other-window")
                                              (minimal-match (0+ nonl))
                                              ")"))
  (define-advice org-babel-execute-src-block (:before (&rest _) d/load-lang)
    "Load src language on demand.

This removes the need to add every language manually to
`org-babel-load-languages'. This also implies that any language
that supports execution can be executed. Executing src blocks is
an active enough action that I'm ok with this."
    (let ((language (intern
                     (org-element-property :language (org-element-at-point)))))
      (message "LANG: %s" language)
      (pcase language
        ('sh (setq language 'shell))
        ('C++ (setq language 'C)))
      (message "LANG: %s" language)
      (unless (alist-get language org-babel-load-languages)
        (add-to-list 'org-babel-load-languages (cons language t))
        (org-babel-do-load-languages
         'org-babel-load-languages
         org-babel-load-languages)))))
#+end_src
***** Appearance
#+begin_src emacs-lisp
(d/after 'org
  ;; appearance
  (setq org-src-fontify-natively t
        org-src-preserve-indentation nil
        org-edit-src-content-indentation 0
        org-fontify-quote-and-verse-blocks t
        org-hide-emphasis-markers nil
        org-startup-with-inline-images t
        org-ellipsis " …"
        org-highlight-latex-and-related '(latex)
        org-pretty-entities nil
        org-hide-leading-stars t
        org-fontify-done-headline t
        org-image-actual-width 500)

  ;; latex
  (setq org-latex-listings t)
  (d/list-prepend! 'org-latex-packages-alist
    '("" "listings")
    '("" "color")
    '("" "tabularx")))
#+end_src
**** Functions
#+begin_src emacs-lisp
(d/after 'org
  (defun d/org-agenda-toggle-date (current-line)
    "Toggle `SCHEDULED' and `DEADLINE' tag in the capture buffer.

Source: https://git.io/vQK0I"
    (interactive "P")
    (save-excursion
      (let ((search-limit (if current-line
                              (line-end-position)
                            (point-max))))

        (if current-line (beginning-of-line)
          (goto-char (point-min)))
        (if (search-forward "DEADLINE:" search-limit t)
            (replace-match "SCHEDULED:")
          (and (search-forward "SCHEDULED:" search-limit t)
               (replace-match "DEADLINE:"))))))

  (defun d/org-insert-list-item-or-self (char)
    "If on column 0, insert space-padded CHAR; otherwise insert CHAR.

This has the effect of automatically creating a properly indented list
leader; like hyphen, asterisk, or plus sign; without having to use
list-specific key maps.

Source: https://git.io/vQK0s"
    (if (bolp)
        (insert (concat char " "))
      (insert char)))

  (defun d/org-swap-tags (tags)
    "Replace any tags on the current headline with TAGS.

The assumption is that TAGS will be a string conforming to Org Mode's
tag format specifications, or nil to remove all tags.

Source: https://git.io/vQKEE"
    (let ((old-tags (org-get-tags-string))
          (tags (if tags
                    (concat " " tags)
                  "")))
      (save-excursion
        (beginning-of-line)
        (re-search-forward
         (concat "[ \t]*" (regexp-quote old-tags) "[ \t]*$")
         (line-end-position) t)
        (replace-match tags)
        (org-set-tags t))))

  (defun d/org-set-tags (tag)
    "Add TAG if it is not in the list of tags, remove it otherwise.

TAG is chosen interactively from the global tags completion table.

Source: https://git.io/vQKEa"
    (interactive
     (list (let ((org-last-tags-completion-table
                  (if (derived-mode-p 'org-mode)
                      (org-uniquify
                       (delq nil (append (org-get-buffer-tags)
                                         (org-global-tags-completion-table))))
                    (org-global-tags-completion-table))))
             (completing-read
              "Tag: " 'org-tags-completion-function nil nil nil
              'org-tags-history))))
    (let* ((cur-list (org-get-tags))
           (new-tags (mapconcat 'identity
                                (if (member tag cur-list)
                                    (delete tag cur-list)
                                  (append cur-list (list tag)))
                                ":"))
           (new (if (> (length new-tags) 1) (concat " :" new-tags ":")
                  nil)))
      (d/org-swap-tags new)))

  (defun d/org-choose-bullet-type ()
    "Change the bullet type for org lists with a prompt."
    (interactive)
    (let ((char (read-char-choice
                 "Bullet type? (-|*|+|1|2|a|b|A|B): "
                 '(?* ?- ?+ ?1 ?2 ?a ?b ?A ?B))))
      (pcase char
        (?1 (org-cycle-list-bullet 3))
        (?2 (org-cycle-list-bullet 4))
        (?a (org-cycle-list-bullet 5))
        (?b (org-cycle-list-bullet 7))
        (?A (org-cycle-list-bullet 6))
        (?B (org-cycle-list-bullet 8))
        (_ (org-cycle-list-bullet (char-to-string char))))))

  (defun d/org-at-openable-item? ()
    "Return non-nil if item is openable. (Think link-like)"
    (let* ((context (org-element-lineage
                     (org-element-context)
                     '(clock footnote-definition footnote-reference headline
                             inlinetask link timestamp)
                     t))
           (type (org-element-type context)))
      (memq type '(footnote-definition
                   footnote-reference
                   headline inlinetask
                   link
                   timestamp))))

  (define-advice org-babel-do-key-sequence-in-edit-buffer (:override (key) d/evil-insert)
    (interactive "kEnter key-sequence to execute in edit buffer: ")
    (org-babel-do-in-edit-buffer
     (evil-insert-state)
     (call-interactively
      (key-binding (or key (read-key-sequence nil)))))))
#+end_src
**** Bindings
#+begin_src emacs-lisp
(d/mode-leader-keys org-mode-map
  "$"  'org-archive-subtree
  "'"  'org-edit-special
  "."  'org-time-stamp
  "/"  'org-sparse-tree
  ":"  'd/org-set-tags
  "-"  'org-decrypt-entry
  "A"  'org-archive-subtree
  "P"  'org-set-property
  "R"  'org-refile
  "^"  'org-sort
  "a"  'org-agenda
  "c"  'org-capture
  "d"  'org-deadline
  "g"  'counsel-org-goto
  "G"  'counsel-org-goto-all
  "l"  'd/org-choose-bullet-type
  "s"  'org-schedule

  "i"  '(:ignore t :wk "insert")
  "ic" 'org-table-insert-column
  "il" 'org-insert-link
  "if" 'org-footnote-new
  "id" 'org-insert-drawer

  "e" 'org-export-dispatch

  "b"  'org-babel-tangle

  "x"  '(:ignore t :wk "text")
  "xe" 'org-emphasize
  "xx" 'org-cut-special
  "xp" 'org-paste-special

  ;; tables
  "t"   '(:ignore t :wk "table")
  "tb"  'org-table-blank-field
  "tc"  'org-table-convert
  "tdc" 'org-table-delete-column
  "tf"  'org-table-eval-formula
  "te"  'org-table-export
  "tic" 'org-table-insert-column
  "tih" 'org-table-insert-hline
  "tiH" 'org-table-hline-and-move
  "tI"  'org-table-import
  "tH"  'org-table-move-column-left
  "tL"  'org-table-move-column-right
  "tn"  'org-table-create
  "tN"  'org-table-create-with-table.el
  "tr"  'org-table-recalculate
  "ts"  'org-table-sort-lines
  "ttf" 'org-table-toggle-formula-debugger
  "tto" 'org-table-toggle-coordinate-overlays
  "tw"  'org-table-wrap-region)

(d/after 'org
  (d/mode-leader-keys org-src-mode
    :definer 'minor-mode
    "'" 'org-edit-src-exit)

  (d/leader-keys org-src-mode
    :definer 'minor-mode
    "fs" 'org-edit-src-save))
#+end_src
**** Setup
***** Agenda
#+begin_src emacs-lisp
(d/after 'org-agenda
  (setq org-habit-graph-column 50))
#+end_src
***** Capture
#+begin_src emacs-lisp
(imap org-capture-mode-mop
  "C-d" 'd/org-agenda-toggle-date)
(nmap org-capture-mode-map
  "C-d" 'd/org-agenda-toggle-date)
#+end_src
***** Org
#+begin_src emacs-lisp
(d/after 'org
  (dolist (char '("+" "-"))
    (define-key org-mode-map (kbd char)
      `(lambda ()
         (interactive)
         (d/org-insert-list-item-or-self ,char)))))
#+end_src
**** Export backends
***** [[https://github.com/kawabata/ox-pandoc][ox-pandoc]]
#+begin_quote
Another org-mode exporter via pandoc.
#+end_quote
Translates Org-mode file to various other formats via Pandoc. Pretty neat.
#+begin_src emacs-lisp
(use-package ox-pandoc
  :after ox
  :demand t
  :if (executable-find "pandoc")
  :custom
  ;; default options for all output formats
  (org-pandoc-options '((standalone . t)
                        (latex-engine . xelatex)
                        (mathjax . t)
                        (parse-raw . t)))
  ;; cancel above settings only for 'docx' format
  (org-pandoc-options-for-docx '((standalone . nil))))
#+end_src
***** [[https://github.com/kaushalmodi/ox-hugo][ox-hugo]]
#+begin_quote
A carefully crafted Org exporter back-end for Hugo
#+end_quote
#+begin_src emacs-lisp
(use-package ox-hugo
  :init
  (d/safe-vars!
    (eval require 'ox-hugo)
    (eval add-hook
          'after-save-hook
          'org-hugo-export-wim-to-md-after-save
          'append 'local)))
#+end_src
**** Enhancements
***** [[https://github.com/snosov1/toc-org][toc-org]]
#+begin_quote
toc-org is an Emacs utility to have an up-to-date table of contents in the org
files without exporting (useful primarily for readme files on GitHub)
#+end_quote
#+begin_src emacs-lisp
(use-package toc-org
  :hook (org-mode . toc-org-enable))
#+end_src
** Bindings
*** Leader
#+begin_src emacs-lisp
(d/leader-keys
  "." 'abort-recursive-edit
  "qf" 'delete-frame
  "qq" 'save-buffers-kill-emacs

  "td" 'toggle-debug-on-error
  "tr" 'd/toggle-rlines

  "&"   'async-shell-command
  ":"   'eval-expression
  "r"   'repeat
  "u"   'universal-argument)
#+end_src
*** Macros
#+begin_src emacs-lisp
(general-def
 "<f11>" 'kmacro-start-macro-or-insert-counter
 "<f12>" 'kmacro-end-or-call-macro)
#+end_src
*** Minibuffer
I like to use ~C-/~ as Evil/Vim's ~C-[~ since I use a Dvorak keyboard, so I like to
also use these keys to quit out of the minibuffer.
#+begin_src emacs-lisp
(general-def
  (minibuffer-local-map
   minibuffer-local-ns-map
   minibuffer-local-completion-map
   minibuffer-local-must-match-map
   minibuffer-local-isearch-map)
  [?\C-/]  'minibuffer-keyboard-quit
  [?\C-_]  'minibuffer-keyboard-quit
  [escape] 'minibuffer-keyboard-quit)

#+end_src
*** universal argument
#+begin_src emacs-lisp
(general-def universal-argument-map
  "SPC u" 'universal-argument-more)
#+end_src
* Help
** Built-in
*** [[elisp:(find-library-other-window%20"man")][man]]
#+begin_quote
browse UNIX manual pages
#+end_quote
#+begin_src emacs-lisp
(use-package man
  :general
  (d/leader-keys
    "hm" 'man)
  :config
  (setq Man-notify-method 'aggressive))
#+end_src
** Packages
*** [[https://github.com/abo-abo/define-word][define-word]]
#+begin_quote
Display the definition of word at point in Emacs
#+end_quote
#+begin_src emacs-lisp
(use-package define-word
  :general
  (d/leader-keys "sw" 'd/define-word)
  :config
  (defun d/define-word (&optional word)
    (interactive)
    (if word
        (define-word word define-word-default-service)
      (let ((word (read-string
                   (concat "Define word ["
                           (if (region-active-p)
                               (buffer-substring (region-beginning) (region-end))
                             (thing-at-point 'word)) "]: ")
                   nil nil
                   (thing-at-point 'word))))
        (define-word word define-word-default-service)))))
#+end_src
*** [[https://github.com/xuchunyang/devdocs.el][devdocs]]
#+begin_quote
Emacs package allowing you to easily search the DevDocs documentation
#+end_quote
#+begin_src emacs-lisp :tangle no
(use-package devdocs
  :general
  (d/leader-keys "hdd"  'devdocs-search))
#+end_src
*** [[https://github.com/Malabarba/emacs-google-this][emacs-google-this]]
#+begin_quote
A set of emacs functions and bindings to google under point.
#+end_quote
#+begin_src emacs-lisp :tangle no
(use-package google-this
  :general
  (d/leader-keys
    "sd" 'ddg-this-search
    "sg" 'google-this-search)
  :config
  (defun ddg-this-parse-and-search-string (text prefix &optional search-url)
    "Convert illegal characters in TEXT to their %XX versions, and then duckduckgo.
PREFIX determines quoting.

Don't call this function directly, it could change depending on
version. Use `ddg-this-string' instead."
    (let* (;; Create the url
           (query-string (google-this--maybe-wrap-in-quotes text prefix))
           ;; Perform the actual search.
           (browse-result (funcall google-this-browse-url-function
                                   (format (or search-url "https://duckduckgo.com/?q=%s")
                                           (url-hexify-string query-string)))))
      ;; Maybe suspend emacs.
      (when google-this-suspend-after-search (suspend-frame))
      ;; Return what browse-url returned (very usefull for tests).
      browse-result))

  (defun ddg-this-pick-term (prefix)
    "Decide what \"this\" and return it.
PREFIX determines quoting."
    (let* ((term (if (region-active-p)
                     (buffer-substring (region-beginning) (region-end))
                   (or (thing-at-point 'symbol)
                       (thing-at-point 'word)
                       (buffer-substring (line-beginning-position)
                                         (line-end-position)))))
           (term (read-string (concat "DuckDuckGo [" term "]: ") nil nil term)))
      term))

  (defun ddg-this-search (prefix &optional search-string)
    "Write and do a DuckDuckGo search.
Interactively PREFIX determines quoting.
Non-interactively SEARCH-STRING is the string to search."
    (interactive "P")
    (let* ((term (ddg-this-pick-term prefix)))
      (if (stringp term)
          (ddg-this-parse-and-search-string term prefix search-string)
        (message "[google-this-string] Empty query.")))))
#+end_src
*** [[https://github.com/atykhonov/google-translate][google-translate]]
#+begin_quote
Emacs interface to Google Translate
#+end_quote
#+begin_src emacs-lisp :tangle no
(use-package google-translate)
#+end_src
*** [[https://github.com/Wilfred/helpful][helpful]]
#+begin_quote
A better Emacs *help* buffer
#+end_quote
#+begin_src emacs-lisp
(use-package helpful
  :custom
  (helpful-short-filenames t)
  :general
  (d/leader-keys
    "hds" 'helpful-symbol
    "hdk" 'helpful-key)
  :init
  ;; (setq find-function-C-source-directory
  ;;       (replace-regexp-in-string
  ;;        (regexp-quote "27.0")
  ;;        "master"
  ;;        find-function-C-source-directory))
  :config
  (evil-set-initial-state 'helpful-mode 'motion))
#+end_src
*** [[https://www.emacswiki.org/emacs/info+.el][info+]]
#+begin_quote
Extensions to info.el.
#+end_quote
#+begin_src emacs-lisp
(use-package info+)
#+end_src
*** [[https://github.com/Wilfred/elisp-refs][elisp-refs]]
#+begin_src emacs-lisp
(use-package elisp-refs
  :config
  (evil-set-initial-state 'elisp-refs-mode 'motion))
#+end_src
*** [[https://github.com/vermiculus/sx.el/][sx]]
#+begin_quote
Stack Exchange for Emacs
#+end_quote
#+begin_src emacs-lisp :tangle no
(use-package sx)
#+end_src
*** [[https://github.com/kuanyui/tldr.el][tldr]]
#+begin_quote
tldr client for Emacs
#+end_quote
#+begin_src emacs-lisp
(use-package tldr
  :general
  (d/leader-keys "ht" 'tldr)
  (nmap tldr-mode-map
    "q" 'quit-window))
#+end_src
** Bindings
Leader bindings
#+begin_src emacs-lisp
(d/leader-keys
  "hc"  '(:ignore t :wk "customize")
  "hca" 'customize-apropos
  "hcf" 'customize-face-other-window
  "hcg" 'customize-group-other-window
  "hcm" 'customize-mode
  "hcv" 'customize-variable-other-window
  "hdV" 'apropos-value
  "hdc" 'describe-char
  "hdm" 'describe-mode
  "hdt" 'describe-theme
  "hn"  'view-emacs-news
  "hi"  'info
  "hs"  (lambda ()
          (interactive)
          (message "%s@%s"
                   (user-login-name)
                   (system-name)))
  "hv"  'version)
#+end_src
* Files/Buffers
** Defaults
*** Buffer names
How to uniquify buffer names.
#+begin_src emacs-lisp
(setq uniquify-buffer-name-style 'forward
      uniquify-strip-common-suffix nil)
#+end_src
*** Frame
If a frame is already open, use it to open files.
#+begin_src emacs-lisp
(setq ns-pop-up-frames nil)
#+end_src
*** Symlinks
Follow symlinks to files under version control because why would I not.
#+begin_src emacs-lisp
(setq vc-follow-symlinks t)
#+end_src
*** Help buffer
Select the help window when opening it (I like this so I can quickly ~q~ out).
#+begin_src emacs-lisp
(setq help-window-select t)
#+end_src
*** Backup
#+begin_src emacs-lisp
(setq version-control t
      delete-old-versions t)
#+end_src
*** Executable
#+begin_src emacs-lisp
(add-hook 'after-save-hook 'executable-make-buffer-file-executable-if-script-p)
#+end_src
** Built-in
*** [[elisp:(find-library-other-window%20"bookmark")][bookmark]]
#+begin_quote
set bookmarks, maybe annotate them, jump to them later
#+end_quote
#+begin_src emacs-lisp
(use-package bookmark
  :straight nil
  :general
  (d/leader-keys
    "fB" 'bookmark-jump-other-window
    "fb" 'bookmark-jump))
#+end_src
*** [[elisp:(find-library-other-window%20"dired")][dired]]
#+begin_quote
directory-browsing commands
#+end_quote
#+begin_src emacs-lisp
(use-package dired
  :straight nil
  :general
  (d/leader-keys
    "ad" 'd/dired-here)
  (d/leader-keys wdired-mode-map
    ;; "fs" 'wdired-finish-edit
    "fs" (lambda ()
           (interactive)
           (message "Use ':w' or variant to finish editing ")))
  (d/mode-leader-keys dired-mode-map
    "h" 'dired-omit-mode
    "d" 'dired-du-mode)
  :custom
  (dired-listing-switches "-lGXhA --group-directories-first")
  (dired-dwim-target t)
  :config
  (nmap dired-mode-map
    "~" 'd/dired-home
    "q" 'd/dired-quit
    ;; got used to this from ranger
    "h" 'dired-up-directory
    "l" 'dired-open-file
    ;; I like inverting these
    "r" 'dired-do-rename
    "R" 'dired-do-redisplay
    ;; evil-dired explicitly uses evil-search
    "n" 'evil-ex-search-next
    "N" 'evil-ex-search-previous)

  (d/after 'dired-async
    (dired-async-mode 1))

  (defun d/dired-quit ()
    (interactive)
    (let ((prev-nondired
           (cl-find-if
            (lambda (spec)
              (not (eq (buffer-local-value 'major-mode (car spec))
                       'dired-mode)))
            (window-prev-buffers))))
      (if prev-nondired
          (switch-to-buffer (car prev-nondired))
        (delete-window))))

  (defun d/dired-here (&optional arg)
    (interactive "P")
    (if arg
        (dired default-directory)
      (dired-other-window default-directory)))

  (defun d/dired-home ()
    (interactive)
    (dired "~/")))
#+end_src
*** [[elisp:(find-library-other-window%20"dired-x")][dired-x]]
#+begin_src emacs-lisp
(use-package dired-x
  :straight nil
  :after dired
  :demand t
  :hook (dired-mode . dired-omit-mode)
  :custom
  (dired-omit-verbose nil)
  (dired-omit-files (rx string-start "." (1+ nonl) string-end))
  (dired-clean-confirm-killing-deleted-buffers nil))
#+end_src
*** [[elisp:(find-library-other-window "ediff")][ediff]]
#+begin_src emacs-lisp
(use-package ediff
  :custom
  (ediff-window-setup-function 'ediff-setup-windows-plain)
  (ediff-split-window-function 'split-window-horizontally)
  (ediff-diff-options "-w"))
#+end_src
*** [[elisp:(find-library-other-window%20"ibuffer")][ibuffer]]
#+begin_quote
operate on buffers like dired
#+end_quote
#+begin_src emacs-lisp
(use-package ibuffer
  :config
  (nmap ibuffer-mode-map
    "TAB" 'ibuffer-toggle-filter-group
    "<backtab>" 'ibuffer-toggle-filter-group
    "*%" 'ibuffer-mark-by-name-regexp)
  (nmap ibuffer-mode-filter-group-map
    "J" 'ibuffer-forward-filter-group
    "K" 'ibuffer-backward-filter-group
    "RET" 'ibuffer-toggle-marks)
  (setq ibuffer-saved-filter-groups
        '(("Default"
           ("Dired"
            (mode . dired-mode))
           ("ERC"
            (mode . erc-mode))
           ("Help"
            (or
             (mode . helpful-mode)
             (mode . Man-mode)
             (predicate  member major-mode
                         ibuffer-help-buffer-modes)))
           ("Package"
            (filename . ".*/dotfiles/emacs.d/straight/.*"))
           ("Built in"
            (and
             (not (mode . eshell-mode))
             (filename . "/gnu.*")))
           ("Magit"
            (derived-mode . magit-mode))
           ("Shell"
            (predicate member major-mode '(eshell-mode term-mode))))))

  (d/hook! ibuffer-mode
    (ibuffer-switch-to-saved-filter-groups "Default"))

  (setq ibuffer-never-show-predicates '("\\*magit-\\(diff\\|process\\):")))
#+end_src
*** [[elisp:(find-library-other-window%20"recentf")][recentf]]
#+begin_src emacs-lisp
(use-package recentf
  :config
  (setq recentf-max-saved-items 50)

  (defun d/recentf-exclude? (f)
    (string-prefix-p
     (expand-file-name "var/" "~/dotfiles/emacs.d")
     (file-truename f))
    (string-match-p "^/\\(ssh\\|sudo\\):"
                    (file-truename f)))

  (d/list-prepend! 'recentf-exclude
    'd/recentf-exclude?))
#+end_src
** Packages
*** [[https://github.com/lunaryorn/osx-trash.el][osx-trash]]
#+begin_quote
Make Emacs' delete-by-moving-to-trash do what you expect it to do on OS X.
#+end_quote
#+begin_src emacs-lisp :tangle no
(use-package osx-trash
  :defer 5
  :if (eq system-type 'darwin)
  :config
  (osx-trash-setup)
  (setq delete-by-moving-to-trash t))
#+end_src
*** dired enhancements
**** [[https://github.com/Fuco1/dired-hacks][dired-hacks]]
#+begin_quote
Collection of useful dired additions
#+end_quote
***** dired-open
#+begin_src emacs-lisp
(use-package dired-open)
#+end_src
***** dired-rainbow
#+begin_src emacs-lisp
(use-package dired-rainbow
  :after dired
  :demand t
  :config
  (dired-rainbow-define-chmod executable-unix "#B8BB26" "-[rw-]+x.*")
  (dired-rainbow-define
   immediate
   "#FABD2F"
   "\\(?:\\(?:R\\(?:EADME\\|eadme\\)\\|readme\\)\\)\\(?:[^ ]*?\\)?\\|Makefile\\|Cargo\\.toml\\|SConstruct\\|CMakeLists\\.txt\\|build\\.gradle\\|Rakefile\\|Gruntfile\\.js\\|Gruntfile\\.coffee")
  (dired-rainbow-define
   image
   "#af5faf"
   ("png" "jpeg" "jpg" "gif" "bmp" "tiff" "tif"
    "ppm" "pgm" "pbm" "pnm" "webp" "raw" "arw"
    "svg" "stl" "eps" "dvi" "ps" "cbr"
    "cbz" "xpm" "ico" "cr2" "orf" "nef"))
  (dired-rainbow-define
   video
   "#af5fff"
   ("avi" "flv" "m2v" "mkv" "mov" "mp4" "mpeg"
    "mpg" "ogm" "ogv" "vob" "wmv" "webm" "m2ts"
    "ts"))
  (dired-rainbow-define
   music
   "#8700d7"
   ("aac" "m4a" "mp3" "ogg" "wma" "mka" "opus"))
  (dired-rainbow-define
   lossless-music
   "#8700ff"
   ("alac" "ape" "flac" "wav"))
  (dired-rainbow-define
   crypto
   "#87afaf"
   ("asc" "enc" "gpg" "pgp" "sig" "signature" "pfx" "p12"))
  (dired-rainbow-define
   document
   "#8787ff"
   ("djvu" "doc" "docx" "dvi" "eml" "eps" "fotd"
    "odp" "odt" "pdf" "ppt" "pptx" "rtf"
    "xls" "xlsx"))
  ;; not working?
  (dired-rainbow-define
   compiled
   "#af875f"
   ("class" "elc" "hi" "o" "pyc")))
#+end_src
***** dired-collpase
#+begin_src emacs-lisp
(use-package dired-collapse
  :hook (dired-mode . d/dired-collapse-mode)
  :config
  (defvar dired-collapse-ignore-dirs
    (list "~/Music"
          "/ssh:"))
  (defun d/dired-collapse-mode ()
    (unless (cl-find-if
             (lambda (dir)
               (string-prefix-p
                (expand-file-name dir)
                (expand-file-name dired-directory)))
             dired-collapse-ignore-dirs)
      (dired-collapse-mode))))
#+end_src
***** dired-subtree
#+begin_src emacs-lisp
(use-package dired-subtree
  :general
  (nmap dired-mode-map
    "TAB" 'dired-subtree-toggle))
#+end_src
**** [[https://github.com/purcell/diredfl][diredfl]]
#+begin_quote
Extra Emacs font lock rules for a more colourful dired
#+end_quote
#+begin_src emacs-lisp
(use-package diredfl
  :hook (dired-mode . diredfl-mode)
  :custom
  (diredfl-ignore-compressed-flag nil))
#+end_src
**** [[https://github.com/emacsmirror/dired-du][dired-du]]
#+begin_quote
Dired with recursive directory sizes
#+end_quote
#+begin_src emacs-lisp
(use-package dired-du
  :straight (:host github :repo "emacsmirror/dired-du")
  :config
  (setq dired-du-size-format t))
#+end_src
*** [[https://github.com/bbatsov/projectile][projectile]]
#+begin_quote
Project Interaction Library for Emacs
#+end_quote
#+begin_src emacs-lisp
(use-package projectile
  :general
  (d/leader-keys
    "p"  '(:ignore t :wk "project")
    "pg" 'projectile-vc
    "pk" 'projectile-kill-buffers
    "po" 'projectile-multi-occur
    "pr" 'projectil-recentf)
  :config
  (defun d/maybe-ignore-project (root)
    (cond ((file-remote-p root)
           t)
          ((string-prefix-p (expand-file-name "~/dotfiles/emacs.d/straight") root)
           (let ((default-directory root))
             (not (string-match-p (regexp-quote "dieggsy")
                                  (shell-command-to-string "git config --get remote.origin.url")))))
          (t nil)))

  (setq projectile-globally-ignored-files '("TAGS" ".DS_Store")
        projectile-ignored-project-function #'d/maybe-ignore-project
        projectile-completion-system 'ivy)
  (projectile-mode))
#+end_src

*** [[https://github.com/ericdanan/counsel-projectile][counsel-projectile]]
#+begin_quote
Ivy UI for Projectile
#+end_quote
#+begin_src emacs-lisp
(use-package counsel-projectile
  :general
  (d/leader-keys
    "pb" 'counsel-projectile-switch-to-buffer
    "pd" 'counsel-projectile-find-dir
    "pf" 'counsel-projectile-find-file
    "pp" 'counsel-projectile
    "ps" 'counsel-projectile-switch-project))
#+end_src

*** [[https://github.com/purcell/whitespace-cleanup-mode][whitespace-cleanup-mode]]
#+begin_src emacs-lisp
(use-package whitespace-cleanup-mode
  :defer 15
  :config
  (global-whitespace-cleanup-mode))
#+end_src
** Functions
*** File/Buffer Manipulation
#+begin_src emacs-lisp
(defun d/copy-file ()
  "Copy file to another location.

Source: https://git.io/vQKES"
  (interactive)
  (call-interactively #'write-file))

(defun d/safe-erase-buffer ()
  "Prompt before erasing buffer.
Source: https://git.io/vQKEd"
  (interactive)
  (if (y-or-n-p (format "Erase content of buffer %s ? " (current-buffer)))
      (progn
        (erase-buffer)
        (message "Buffer erased."))
    (message "erase-buffer cancelled")))

(defun d/download-file (&optional url name)
  "Download a file from url to specified path."
  (interactive)
  (let* ((file-url (or url (read-from-minibuffer "URL: ")))
         (file-name
          (or name
              (counsel-find-file
               (file-name-nondirectory file-url)))))
    (url-copy-file file-url file-name)))

(defun d/gpl-me ()
  (interactive)
  (d/download-file "https://www.gnu.org/licenses/gpl-3.0.md"
                   (concat default-directory "LICENSE.md")))
#+end_src
*** Switching
#+begin_src emacs-lisp
(defun d/switch-to-scratch ()
  "Switch to scratch buffer."
  (interactive)
  (switch-to-buffer "*scratch*"))

(defun d/switch-to-star ()
  "Switch to '*' buffers."
  (interactive)
  (let ((ivy-initial-inputs-alist '((ivy-switch-buffer . "^*"))))
    (ivy-switch-buffer)))

(defun d/switch-to-customize ()
  "Switch to \"Customize\" buffers."
  (interactive)
  (let ((ivy-initial-inputs-alist '((ivy-switch-buffer . "^*customize "))))
    (ivy-switch-buffer)))

(defun d/switch-to-messages ()
  "Switch to *Messages* buffer."
  (interactive)
  (switch-to-buffer "*Messages*"))
#+end_src
*** Narrowing
#+begin_src emacs-lisp
(defun d/narrow-and-set-normal ()
  "Narrow to the region and, if in a visual mode, set normal mode.

Source: https://git.io/vQKEx"
  (interactive)
  (narrow-to-region (region-beginning) (region-end))
  (if (string= evil-state "visual")
      (progn (evil-normal-state nil)
             (evil-goto-first-line))))

(defun d/narrow-to-region-or-subtree ()
  "Narrow to a region, if set, otherwise to an Org subtree, if present.

Source: https://git.io/vQKuf"
  (interactive)
  (if (and mark-active
           (not (= (region-beginning) (region-end))))
      (d/narrow-and-set-normal)
    (if (derived-mode-p 'org-mode)
        (org-narrow-to-subtree))))

(defun d/narrow-dwim ()
  "Narrow to a thing or widen based on context.
Attempts to follow the Do What I Mean philosophy.

Source: https://git.io/vQKuU"
  (interactive)
  (if (buffer-narrowed-p)
      (widen)
    (d/narrow-to-region-or-subtree)))
#+end_src
** Bindings
#+begin_src emacs-lisp
(d/leader-keys
  "b*" 'd/switch-to-star
  "bC" 'd/switch-to-customize
  "bK" 'kill-buffer
  "bM" 'd/switch-to-messages
  ;; "br" 'revert-buffer
  "br" (lambda ()
         (interactive)
         (message "Use ':e' to revert buffer."))
  "bR" 'rename-buffer
  "bS" 'd/switch-to-scratch
  "bc" 'clone-indirect-buffer-other-window
  "be" 'd/safe-erase-buffer
  "bi" 'ibuffer
  ;; "bk" 'kill-this-buffer
  "bk" (lambda ()
         (interactive)
         (message "Use ':bk' to kill buffer."))
  "bm" 'kill-matching-buffers
  "bq" 'kill-buffer-and-window
  "bv" 'view-mode

  "fc" 'd/copy-file
  ;; "fs" 'save-buffer
  "fs" (lambda ()
         (interactive)
         (message "Use ':w' to save buffer."))

  "nf" 'narrow-to-defun
  "nn" 'd/narrow-dwim
  "np" 'narrow-to-page
  "nr" 'narrow-to-region)

(evil-ex-define-cmd "view" 'evil-view)
(evil-ex-define-cmd "bk[ill]" 'kill-this-buffer)
(evil-ex-define-cmd "wk[ill]" (lambda () (interactive)
                                (save-buffer)
                                (kill-this-buffer)))
#+end_src
* Editing
** Defaults
*** Default mode
Text-mode is nicer than fundamental-mode, or so I hear.
#+begin_src emacs-lisp
(setq-default major-mode 'text-mode)
#+end_src
*** Fill
Fill column default, and use auto-fill for text-mode (and derived modes, such
as org-mode, markdown, etc.).
#+begin_src emacs-lisp
(setq-default fill-column 79)
(add-hook 'text-mode-hook 'auto-fill-mode)
#+end_src
*** Input
TeX input is /really/ useful for inputing special characters. Setting it as
default makes it quickly available with ~C-\~, or ~toggle-input-method~.

This way, when you need to input a greek letter or an em-dash or something,
type ~C-\~, use latex input, and see the automagic replacement happen in all its
glory.
#+begin_src emacs-lisp
(setq default-input-method "TeX")
#+end_src
*** Sentence
Who uses double spaces between sentences?
#+begin_src emacs-lisp
(setq sentence-end-double-space nil)
#+end_src
*** Tab
Dear god I hate tabs. Also, four spaces is a good indentation default.
#+begin_src emacs-lisp
(setq-default indent-tabs-mode nil
              tab-width 4)
#+end_src
** Tools
*** Built-in
**** [[elisp:(find-library-other-window "paren")][paren]]
#+begin_quote
highlight matching paren
#+end_quote
#+begin_src emacs-lisp
(use-package paren
  :straight nil
  :hook ((emacs-lisp-mode scheme-mode lisp-mode) . show-paren-mode))
#+end_src
**** [[elisp:(find-library-other-window "hippie-exp")][hippie-expand]]
#+begin_quote
expand text trying various ways to find its expansion
#+end_quote
#+begin_src emacs-lisp
(use-package hippie-exp
  :straight nil
  :general
  ("M-/" #'hippie-expand))
#+end_src
*** Packages
**** [[https://github.com/abo-abo/auto-yasnippet][auto-yasnippet]]
#+begin_quote
quickly create disposable yasnippets
#+end_quote
#+begin_src emacs-lisp
(use-package auto-yasnippet)
#+end_src
**** [[https://github.com/company-mode/company-mode][company-mode]]
#+begin_quote
Modular in-buffer completion framework for Emacs
#+end_quote
Supposedly better than autocomplete.
#+begin_src emacs-lisp
(use-package company
  :defer 5
  :general
  (company-active-map
   [tab] 'company-complete-common-or-cycle)
  :custom
  (company-idle-delay 0.3)
  (company-minimum-prefix-length 1)
  (company-selection-wrap-around t)
  (company-dabbrev-char-regexp "\\sw\\|\\s_\\|[-_]")
  :config
  (defun company-mode/backend-with-yas (backend)
    "Source: https://git.io/vQKE6"
    (if (and (listp backend) (member 'company-yasnippet backend))
        backend
      (append (if (consp backend) backend (list backend))
              '(:with company-yasnippet))))
  (setq company-backends (mapcar #'company-mode/backend-with-yas
                                 company-backends))
  (global-company-mode t))
#+end_src
**** [[https://github.com/company-mode/company-statistics][company-statistics]]
#+begin_quote
Sort completion candidates by previous completion choices
#+end_quote
#+begin_src emacs-lisp
(use-package company-statistics
  :hook (company-mode . company-statistics-mode))
#+end_src
**** [[https://github.com/gabesoft/evil-mc][evil-mc]]
#+begin_quote
Multiple cursors implementation for evil-mode
#+end_quote
#+begin_src emacs-lisp
(use-package evil-mc
  :general
  (d/leader-keys
    "xM"  '(:def d/mc/body :wk "multiple-cursors"))
  :config
  (global-evil-mc-mode)
  (push 'evil-smartparens-mode evil-mc-incompatible-minor-modes)
  (push 'fci-mode evil-mc-incompatible-minor-modes)
  (defhydra d/mc (:post (evil-mc-undo-all-cursors))
    "
╭─────────╮
│ evil-mc │
└─────────┴─────────────────────────────────────────────────────────────
 [_mm_] make+next-match [_sm_] skip+next-match [_h_] here   [_P_] print-pattern
 [_mM_] make+prev-match [_sM_] skip+prev-match [_a_] all
 [_mc_] make+next-cur   [_sc_] skip+next-cur   [_p_] pause
 [_mC_] make+prev-cur   [_sC_] skip+prev-cur   [_r_] resume
 [_mf_] make+first-cur                       [_u_] undo-all
 [_ml_] make+last-cur
────────────────────────────────────────────────────────────────────────
"
    ("mm" evil-mc-make-and-goto-next-match)
    ("mM" evil-mc-make-and-goto-prev-match)
    ("mc" evil-mc-make-and-goto-next-cursor)
    ("mC" evil-mc-make-and-goto-prev-cursor)
    ("mf" evil-mc-make-and-goto-first-cursor)
    ("ml" evil-mc-make-and-goto-last-cursor)
    ("sm" evil-mc-skip-and-goto-next-match)
    ("sM" evil-mc-skip-and-goto-prev-match)
    ("sc" evil-mc-skip-and-goto-next-cursor)
    ("sC" evil-mc-skip-and-goto-prev-cursor)
    ("h" evil-mc-make-cursor-here)
    ("a" evil-mc-make-all-cursors)
    ("p" evil-mc-pause-cursors)
    ("r" evil-mc-resume-cursors)
    ("u" evil-mc-undo-all-cursors)
    ("P" evil-mc-print-pattern)))
#+end_src
**** [[https://github.com/hlissner/evil-multiedit][evil-multiedit]]
#+begin_quote
Multiple cursors for evil-mode, based on iedit
#+end_quote
#+begin_src emacs-lisp
(use-package evil-multiedit
  :general
  (d/leader-keys
    "xm"  '(:def d/multiedit/body :wk "multiedit"))
  :config
  (evil-ex-define-cmd "ie[dit]" 'evil-multiedit-ex-match)

  (defhydra d/multiedit (:color pink)
    "
╭───────────╮
│ multiedit │
└───────────┴──────────────────────────────────────────────────────────
 [_>_] next [_m_] match+next [_s_] symbol+next [_a_] all     [_t_] toggle-marker
 [_<_] prev [_M_] match+prev [_S_] symbol+prev [_r_] restore [_T_] toggle-region
───────────────────────────────────────────────────────────────────────
"
    (">"   evil-multiedit-next)
    ("<"   evil-multiedit-prev)
    ("m"   evil-multiedit-match-and-next)
    ("M"   evil-multiedit-match-and-prev)
    ("s"   evil-multiedit-match-symbol-and-next)
    ("S"   evil-multiedit-match-symbol-and-prev)
    ("a"   evil-multiedit-match-all)
    ("r"   evil-multiedit-restore)
    ("t"   evil-multiedit-toggle-or-restrict-region)
    ("T"   evil-multiedit-toggle-marker-here)
    ("q"   evil-multiedit-abort :exit t)))
#+end_src

**** [[https://www.emacswiki.org/emacs/FlySpell][flyspell]]
#+begin_quote
On-the-fly spell checker
#+end_quote
#+begin_src emacs-lisp
(use-package flyspell
  :general
  (d/mode-leader-keys text-mode-map
    "f" '(:def d/flyspell/body :wk "flyspell"))
  :config
  (defun d/flyspell-add-to-dictionary ()
    "Add word at point to flyspell dictionary.

Source: http://tinyurl.com/k8g9sex"
    (interactive)
    (let ((current-location (point))
          (word (flyspell-get-word)))
      (when (consp word)
        (flyspell-do-correct 'save
                             nil
                             (car word)
                             current-location
                             (caddr word)
                             (caddr word)
                             current-location))))

  (defhydra d/flyspell ()
    "
╭──────────╮
│ flyspell │
└──────────┴───────────────────────────────────────────────────────
 [_>_] goto-next [_c_] correct-next [_a_] auto-correct-next [_b_] buffer
 [_<_] goto-prev [_C_] correct-prev [_A_] auto-correct-prev [_d_] dict add
───────────────────────────────────────────────────────────────────
"
    (">" flyspell-goto-next-error)
    ("<" flyspell-goto-prev-error)
    ("c" flyspell-correct-next)
    ("C" flyspell-correct-previous)
    ("a" flyspell-auto-correct-word)
    ("A" flyspell-auto-correct-previous-word)
    ("b" flyspell-buffer)
    ("d" d/flyspell-add-to-dictionary)))
#+end_src
**** [[https://github.com/d12frosted/flyspell-correct][flyspell-correct-ivy]]
#+begin_quote
Correcting words with flyspell via custom interface.
#+end_quote
#+begin_src emacs-lisp
(use-package flyspell-correct-ivy
  :defer 15)
#+end_src
**** [[https://github.com/syohex/emacs-fontawesome][fontawesome]]
#+begin_src emacs-lisp
(use-package fontawesome)
#+end_src
**** [[elisp:(find-library%20"counsel-weather-icons")][counsel-weather-icons]]
#+begin_src emacs-lisp
(use-package counsel-weather-icons
  :commands counsel-weather-icons
  :straight nil)
#+end_src
**** [[https://github.com/nflath/hungry-delete][hungry-delete]]
#+begin_src emacs-lisp
(use-package hungry-delete
  :defer 5
  :config
  (global-hungry-delete-mode))
#+end_src
**** [[https://github.com/abo-abo/lispy][lispy]]
#+begin_quote
short and sweet LISP editing
#+end_quote
#+begin_src emacs-lisp
(use-package lispy
  :hook ((lispy-mode . d/lispy-fontify-headlines)
         ((emacs-lisp-mode
           lisp-mode
           scheme-mode
           geiser-repl-mode
           ielm-mode)
          . lispy-mode))
  :general
  (lispy-mode-map-lispy
   [tab] (general-predicate-dispatch nil
           (save-excursion (beginning-of-line) (looking-at lispy-outline))
           'd/lispy-cycle))
  :custom
  (lispy-colon-no-space-regex
   '((scheme-mode . ".")
     (geiser-repl-mode . ".")
     (lisp-mode . "\\s-\\|[:^?#]\\|\\(?:\\s([[:word:]-]*\\)")))
  :config
  (d/list-prepend! 'lispy-parens-preceding-syntax-alist
    '(scheme-mode "[#`',@]+")
    '(geiser-repl-mode "[#`',@]+"))

  (defun d/lispy-cycle ()
    (interactive)
    (save-excursion (beginning-of-line) (lispy-tab)))

  (define-advice lispy-meta-return (:after nil d/eol)
    (end-of-line))

  (defun d/lispy-fontify-headlines ()
    (interactive)
    "Calculate heading regexps for font-lock mode."
    (let* ((heading-1-regexp ";;\\* \\(.*\\)")
           (heading-2-regexp ";;\\*\\* \\(.*\\)")
           (heading-3-regexp ";;\\*\\*\\* \\(.*\\)")
           (heading-4-regexp ";;\\*\\*\\*\\* \\(.*\\)")
           (heading-5-regexp ";;\\*\\*\\*\\*\\* \\(.*\\)")
           (heading-6-regexp ";;\\*\\*\\*\\*\\*\\* \\(.*\\)")
           (heading-7-regexp ";;\\*\\*\\*\\*\\*\\*\\* \\(.*\\)")
           (heading-8-regexp ";;\\*\\*\\*\\*\\*\\*\\*\\* \\(.*\\)"))
      (font-lock-add-keywords
       nil
       `((,heading-1-regexp 1 'org-level-1 t)
         (,heading-2-regexp 1 'org-level-2 t)
         (,heading-3-regexp 1 'org-level-3 t)
         (,heading-4-regexp 1 'org-level-4 t)
         (,heading-5-regexp 1 'org-level-5 t)
         (,heading-6-regexp 1 'org-level-6 t)
         (,heading-7-regexp 1 'org-level-7 t)
         (,heading-8-regexp 1 'org-level-8 t)))
      (if (fboundp #'font-lock-flush)
          (font-lock-flush)
        ;; Copied from Emacs 25 font-lock.el, changed to call
        ;; `jit-lock-refontify' directly
        (and font-lock-mode
             font-lock-fontified
             (jit-lock-refontify))))))
#+end_src
**** [[https://github.com/noctuid/lispyville][lispyville]]
#+begin_quote
lispy + evil = lispyville
#+end_quote
#+begin_src emacs-lisp
(use-package lispyville
  :hook (lispy-mode . lispyville-mode)
  :custom
  (lispyville-key-theme '(operators
                          c-w
                          escape
                          additional-movement
                          additional
                          slurp/barf-cp))
  :config
  (lispy-define-key lispy-mode-map "v" #'lispyville-toggle-mark-type)
  (setq lispyville-barf-stay-with-closing t))
#+end_src
**** [[https://github.com/Fuco1/smartparens][smartparens]]
#+begin_quote
Minor mode for Emacs that deals with parens pairs and tries to be smart about
it.
#+end_quote
#+begin_src emacs-lisp
(use-package smartparens
  :hook (((prog-mode conf-mode eval-expression-minibuffer-setup)
          . d/turn-on-smartparens-strict-mode)
         (eshell-mode . smartparens-mode))
  :defer 15
  :commands sp-end-of-sexp
  :general
  (d/leader-keys
    "xp"  '(:def d/smartparens/body :wk "smartparens"))
  :custom
  (sp-ignore-modes-list
   '(scheme-mode
     emacs-lisp-mode
     lisp-mode
     geiser-repl-mode
     inferior-emacs-lisp-mode))
  :init
  (defun d/turn-on-smartparens-strict-mode ()
    (when (not (member major-mode sp-ignore-modes-list))
      (turn-on-smartparens-strict-mode)))
  :config
  (require 'smartparens-config)
  (smartparens-global-mode)
  (show-smartparens-global-mode)
  (setq sp-escape-quotes-after-insert nil)
  (let ((modes '(text-mode
                 org-mode
                 markdown-mode
                 minibuffer-inactive-mode
                 html-mode)))
    (sp-local-pair modes "'" nil :actions nil)
    (sp-local-pair modes "`" nil :actions nil))

  (defhydra d/smartparens ()
    "
╭─────────────╮
│ smartparens │
└─────────────┴───────────────────
 [_>_] slurp→ [_S_] slurp← [_r_] rewrap
 [_<_] barf←  [_B_] barf→  [_u_] unwrap
──────────────────────────────────
"
    ("r"  sp-rewrap-sexp)
    ("u"  sp-unwrap-sexp)
    ("<"  sp-forward-barf-sexp)
    ("B"  sp-backward-barf-sexp)
    (">"  sp-forward-slurp-sexp)
    ("S"  sp-backward-slurp-sexp)))
#+end_src
**** [[https://github.com/expez/evil-smartparens][evil-smartparens]]
#+begin_src emacs-lisp
(use-package evil-smartparens
  :init
  (make-variable-buffer-local 'evil-smartparens-mode-map)
  :hook (smartparens-enabled . evil-smartparens-mode))
#+end_src
**** [[https://www.emacswiki.org/emacs/UndoTree][undo-tree]]
#+begin_quote
Treat undo history as a tree
#+end_quote
Kind of makes undo like git.
#+begin_src emacs-lisp
(use-package undo-tree
  :hook (org-mode . undo-tree-mode)
  :general
  (d/leader-keys "au" 'undo-tree-visualize)
  :config
  (setq undo-tree-visualizer-timestamps t
        undo-tree-auto-save-history t))
#+end_src
**** [[https://github.com/purcell/unfill][unfill]]
#+begin_quote
Functions providing the inverse of Emacs' fill-paragraph and fill-region
#+end_quote
#+begin_src emacs-lisp
(use-package unfill
  :general
  (d/leader-keys "xq" 'unfill-toggle)
  ([remap fill-paragraph] 'unfill-toggle))
#+end_src
**** [[https://github.com/joaotavora/yasnippet][yasnippet]]
#+begin_quote
A template system for Emacs
#+end_quote
#+begin_src emacs-lisp
(use-package yasnippet
  :defer 5
  :general (d/leader-keys "iy" 'yas-insert-snippet)
  :config
  ;; (imap yas-minor-mode-map
  ;;   "SPC" yas-maybe-expand
  ;;   "S-SPC" (lambda () (interactive) (insert " ")))
  (setq yas-key-syntaxes (remove "w" yas-key-syntaxes))
  (yas-global-mode 1))
#+end_src
***** [[https://github.com/haskell/haskell-snippets][haskell-snippets]]
#+begin_src emacs-lisp
(use-package haskell-snippets)
#+end_src
*** Functions
**** Move text
#+begin_src emacs-lisp
(defun d/transpose-chars (arg)
  "Move character at point forward one character.
With prefix arg ARG, effect is to take character at point and
drag it forward past ARG other characters (backward if ARG
negative)."
  (interactive "P")
  (forward-char)
  (if arg
      (transpose-chars arg)
    (transpose-chars 1))
  (backward-char))

(defun d/backward-transpose-chars (arg)
  "Move character at point backward one character.
With prefix arg ARG, effect is to take character at point and
drag it backward past ARG other characters (backward if ARG
negative)."
  (interactive "P")
  (d/transpose-chars (- (or arg 1))))

(defun d/backward-transpose-words (arg)
  "Interchange words around point, leaving point at end of them.
With prefix arg ARG, effect is to take word before or around
point and drag it forward past ARG other words (backward if ARG
negative). If ARG is zero, the words around or after point and
around or after mark are interchanged."
  (interactive "P")
  (transpose-words (- (or arg 1))))

(defun d/transpose-lines (arg)
  (interactive "P")
  (save-excursion
    (end-of-line)
    (when (eobp) (insert "\n")))
  (forward-line 1)
  (with-demoted-errors "%s"
    (transpose-lines (or arg 1)))
  (forward-line -1))

(defun d/backward-transpose-lines (arg)
  (interactive "P")
  (d/transpose-lines (- (or arg 1))))
#+end_src
**** Paragraph
#+begin_src emacs-lisp
(defun d/paragraphize ()
  "Remove empty newlines from region."
  (interactive)
  (if (region-active-p)
      (flush-lines "^$" (region-beginning) (region-end))
    (message "No region active.")))
#+end_src
**** Url
#+begin_src emacs-lisp
(defun d/shorten-url-at-point ()
  "Shorten the url at point using the github url shortener or the TinyURL api.

Source: http://tinyurl.com/l8z7vph"
  (interactive)
  (if (thing-at-point 'url)
      (let* ((long-url (thing-at-point 'url))
             (short-url
              (cond ((save-match-data
                       (string-match "https://\\(github.com\\|gist.github.com\\)" long-url))
                     (let ((info (shell-command-to-string
                                  (format "curl -i \"https://git.io\" -F \"url=%s\""
                                          long-url))))
                       (save-match-data
                         (and (string-match "Location: \\(.*?\\)" info)
                              (match-string 1 info)))))
                    (t
                     (shell-command-to-string
                      (format "curl -s \"http://tinyurl.com/api-create.php?url=%s\""
                              (url-hexify-string long-url))))))
             (bounds (bounds-of-thing-at-point 'url)))
        (kill-region (car bounds) (cdr bounds))
        (insert short-url))
    (error "No url at point.")))

(defun d/expand-url-at-point ()
  (interactive)
  (if (thing-at-point 'url)
      (let* ((short-url (thing-at-point 'url))
             (long-url (shell-command-to-string (format "curl -Ls -o /dev/null -w '%%{url_effective}' \"%s\""
                                                        short-url)))
             (bounds (bounds-of-thing-at-point 'url)))
        (kill-region (car bounds) (cdr bounds))
        (insert long-url))
    (error "No url at point.")))
#+end_src
*** Hydras
**** Transpose
#+begin_src emacs-lisp
(defhydra d/transpose ()
  "
╭───────────╮
│ transpose │
└───────────┴────────────────────────────
 [_c_] char→ [_j_] line↑  [_w_] word→ [_s_] sexp
 [_C_] char← [_k_] line↓  [_W_] word←
─────────────────────────────────────────
"
  ("c" d/transpose-chars)
  ("C" d/backward-transpose-chars)
  ("j" d/transpose-lines)
  ("k" d/backward-transpose-lines)
  ("w" transpose-words)
  ("W" d/backward-transpose-words)
  ("s" transpose-sexps :exit t))
#+end_src
**** Justify
#+begin_src emacs-lisp
(defhydra d/justify (:exit t)
  "
╭─────────╮
│ justify │
└─────────┴────────────────────
 [_<_] left  [_c_] center [_n_] none
 [_>_] right [_f_] full
───────────────────────────────
"
  (">" set-justification-left)
  ("<" set-justification-right)
  ("c" set-justification-center)
  ("f" set-justification-full)
  ("n" set-justification-none))
#+end_src
** Modes
*** [[https://github.com/Kitware/CMake][cmake-mode]]
#+begin_src emacs-lisp :tangle no
(use-package cmake-mode)
#+end_src
*** conf-mode
#+begin_src emacs-lisp
(d/hook! conf-mode
  (d/setup-prog-mode))
(add-to-list 'auto-mode-alist '("\\.service\\'" . conf-mode))
#+end_src
*** [[http://elpa.gnu.org/packages/csv-mode.html][csv-mode]]
#+begin_quote
Major mode for editing comma/char separated values
#+end_quote
Eh, wanted to try a simpler way of editing csv files. (Excel and Numbers both
kinda suck at this, LibreOffice was slightly better.) Haven't used this much.
#+begin_src emacs-lisp
(use-package csv-mode
  :mode "\\.csv\\'"
  :config
  (add-hook 'csv-mode-hook #'csv-align-fields))
#+end_src
*** [[https://github.com/joshwnj/json-mode][json-mode]]
#+begin_quote
Major mode for editing JSON files with emacs
#+end_quote
#+begin_src emacs-lisp
(use-package json-mode
  :mode "\\.json\\'")
#+end_src
*** [[http://jblevins.org/projects/markdown-mode/][markdown-mode]]
#+begin_quote
Major mode for editing Markdown-formatted text
#+end_quote
Syntax highlighting for markdown files.
#+begin_src emacs-lisp
(use-package markdown-mode
  :mode "\\.md\\'")
#+end_src
*** [[https://github.com/yoshiki/yaml-mode][yaml-mode]]
#+begin_quote
The emacs major mode for editing files in the YAML data serialization format.
#+end_quote
#+begin_src emacs-lisp
(use-package yaml-mode
  :mode "\\.yml\\'")
#+end_src
** Bindings
Make indent-rigidly more vimmy.
#+begin_src emacs-lisp
(general-def indent-rigidly-map
  "h" 'indent-rigidly-left
  "l" 'indent-rigidly-right
  "H" 'indent-rigidly-left-to-tab-stop
  "L" 'indent-rigidly-right-to-tab-stop)
#+end_src
Leader keys
#+begin_src emacs-lisp
(d/leader-keys
  "xii" 'indent-rigidly
  "xir" 'indent-region
  "xj"  '(:def d/justify/body :wk "justify")
  "xls" 'sort-lines
  "xt"  '(:def d/transpose/body :wk "transpose")
  "xc"  'count-words

  "xs"  'd/shorten-url-at-point
  "xe"  'd/expand-url-at-point

  "im"  'insert-kbd-macro)
#+end_src
* Navigation
** Defaults
*** Scrolling
Scroll one line at a time, and only scroll the current line when moving past
right boundary.
#+begin_src emacs-lisp
(setq scroll-step 1
      scroll-conservatively 10000
      auto-hscroll-mode 'current-line)
#+end_src
*** Mouse Scroll
Smoother mouse scrolling, which is now irrelevant to me since I've disabled the
mouse in emacs.
#+begin_src emacs-lisp
(setq mouse-wheel-scroll-amount '(2 ((shift) . 1) ((control) . nil))
      mouse-wheel-progressive-speed nil)
#+end_src
** Functions
#+begin_src emacs-lisp
(defun d/toggle-window-split ()
  "Switch between vertical and horizontal window split.

Source: http://tinyurl.com/k7s96fa"
  (interactive)
  (if (= (count-windows) 2)
      (let* ((this-win-buffer (window-buffer))
             (next-win-buffer (window-buffer (next-window)))
             (this-win-edges (window-edges (selected-window)))
             (next-win-edges (window-edges (next-window)))
             (this-win-2nd (not (and (<= (car this-win-edges)
                                         (car next-win-edges))
                                     (<= (cadr this-win-edges)
                                         (cadr next-win-edges)))))
             (splitter
              (if (= (car this-win-edges)
                     (car (window-edges (next-window))))
                  #'split-window-horizontally
                #'split-window-vertically)))
        (delete-other-windows)
        (let ((first-win (selected-window)))
          (funcall splitter)
          (if this-win-2nd (other-window 1))
          (set-window-buffer (selected-window) this-win-buffer)
          (set-window-buffer (next-window) next-win-buffer)
          (select-window first-win)
          (if this-win-2nd (other-window 1))))))

(defun d/split-vert-focus ()
  "Split window vertically and move focus to other window."
  (interactive)
  (split-window-right)
  (other-window 1))

(defun d/split-horz-focus ()
  "Split window horizontally and move focus to other window."
  (interactive)
  (split-window-below)
  (other-window 1))
#+end_src
** Built-in
*** [[elisp:(find-library-other-window%20"goto-addr")][goto-addr]]
#+begin_quote
click to browse URL or to send to e-mail address
#+end_quote
#+begin_src emacs-lisp
(use-package goto-addr
  :straight nil
  :hook (((help-mode org-mode text-mode) . goto-address-mode)
         ((prog-mode conf-mode) . goto-address-prog-mode)))
#+end_src
*** [[elisp:(find-library-other-window%20"display-line-numbers")][display-line-numbers]]
#+begin_src emacs-lisp
(use-package display-line-numbers
  :if (version<= "26" emacs-version)
  :straight nil
  :hook ((prog-mode conf-mode) . display-line-numbers-mode)
  :custom
  (display-line-numbers-type 'relative)
  (display-line-numbers-width-start t)
  (display-line-numbers-grow-only t)
  :config
  (defun d/toggle-rlines ()
    "Toggle relative line numbers."
    (interactive)
    (if (eq display-line-numbers 'relative)
        (setq display-line-numbers t)
      (setq display-line-numbers 'relative))))
#+end_src
*** [[elisp:(find-library%20"winner")][winner]]
#+begin_src emacs-lisp
(use-package winner
  :defer 15
  :general
  (evil-window-map
   "<right>" 'd/winner/winner-redo
   "<left>" 'd/winner/winner-undo)
  :config
  (winner-mode)
  (defhydra d/winner ()
    "
╭────────╮
│ winner │
└────────┴────────────────────
 [_<left>_] undo [_<right>_] redo
──────────────────────────────
"
    ("<right>" winner-redo)
    ("<left>" winner-undo)))
#+end_src
** Packages
*** [[https://github.com/abo-abo/ace-window][ace-window]]
#+begin_quote
Quickly switch windows in Emacs
#+end_quote
#+begin_src emacs-lisp
(use-package ace-window
  :general
  (nmap :keymaps 'override "\\" 'ace-window)
  (mmap :keymaps 'override "\\" 'ace-window)
  :config
  (setq aw-keys (string-to-list "aoeuidhtns")
        aw-scope 'visible))
#+end_src
*** [[https://github.com/abo-abo/avy][avy]]
#+begin_quote
Jump to things in Emacs tree-style
#+end_quote
#+begin_src emacs-lisp
(use-package avy
  :general
  (d/leader-keys
    "jc" 'avy-goto-char-2
    "jl" 'avy-goto-line
    "jw" 'avy-goto-word-1)
  :config
  (setq avy-keys (string-to-list "aoeuidhtns")))
#+end_src
*** [[https://github.com/jacktasia/dumb-jump][dumb-jump]]
#+begin_quote
an Emacs "jump to definition" package
#+end_quote
#+begin_src emacs-lisp
(use-package dumb-jump
  :general
  (d/leader-keys
    "jE" 'dumb-jump-go-prefer-external-other-window
    "jG" 'dumb-jump-go-other-window
    "jb" 'dumb-jump-back
    "je" 'dumb-jump-go-prefer-external
    "jg" 'dumb-jump-go)
  :config
  (setq dumb-jump-selector 'ivy
        dumb-jump-prefer-searcher 'rg))
#+end_src

*** [[https://github.com/wasamasa/eyebrowse][eyebrowse]]
#+begin_quote
A simple-minded way of managing window configs in emacs
#+end_quote
#+begin_src emacs-lisp
(use-package eyebrowse
  :general
  ("<f10>" 'eyebrowse-switch-to-window-config-0
   "<f1>" 'eyebrowse-switch-to-window-config-1
   "<f2>" 'eyebrowse-switch-to-window-config-2
   "<f3>" 'eyebrowse-switch-to-window-config-3
   "<f4>" 'eyebrowse-switch-to-window-config-4
   "<f5>" 'eyebrowse-switch-to-window-config-5
   "<f6>" 'eyebrowse-switch-to-window-config-6
   "<f7>" 'eyebrowse-switch-to-window-config-7
   "<f8>" 'eyebrowse-switch-to-window-config-8
   "<f9>" 'eyebrowse-switch-to-window-config-9)
  (d/leader-keys
    "e"  '(:ignore t :wk "eyebrowse")
    "es" 'eyebrowse-switch-to-window-config
    "el" 'eyebrowse-next-window-config
    "eh" 'eyebrowse-prev-window-config
    "er" 'eyebrowse-rename-window-config
    "ec" 'eyebrowse-close-window-config
    "e'" 'eyebrowse-last-window-config
    "e0" 'eyebrowse-switch-to-window-config-0
    "e1" 'eyebrowse-switch-to-window-config-1
    "e2" 'eyebrowse-switch-to-window-config-2
    "e3" 'eyebrowse-switch-to-window-config-3
    "e4" 'eyebrowse-switch-to-window-config-4
    "e5" 'eyebrowse-switch-to-window-config-5
    "e6" 'eyebrowse-switch-to-window-config-6
    "e7" 'eyebrowse-switch-to-window-config-7
    "e8" 'eyebrowse-switch-to-window-config-8
    "e9" 'eyebrowse-switch-to-window-config-9)
  :config
  (setq eyebrowse-wrap-around t
        eyebrowse-new-workspace t
        eyebrowse-switch-back-and-forth t)

  (eyebrowse-mode))
#+end_src
*** [[https://github.com/vspinu/imenu-anywhere][imenu-anywhere]]
#+begin_quote
ido/ivy/helm imenu tag selection across buffers with the same mode/project
etc
#+end_quote
imenu on steroids.
#+begin_src emacs-lisp
(use-package imenu-anywhere)
#+end_src
*** [[https://github.com/wasamasa/shackle][shackle]]
#+begin_src emacs-lisp
(use-package shackle
  :defer 15
  :config
  (setq shackle-rules '((compilation-mode :ignore t)
                        (inferior-emacs-lisp-mode :popup t)))
  (shackle-mode))
#+end_src
*** [[https://github.com/cyrus-and/zoom][zoom]]
#+begin_quote
Fixed and automatic balanced window layout for Emacs
#+end_quote
#+begin_src emacs-lisp :tangle no
(use-package zoom
  :config
  (setq zoom-size '(0.618 . 0.618)
        zoom-ignored-buffer-names '(" *which-key*"
                                    "*Calculator*"
                                    "*Calc Trail*")))
#+end_src
*** [[https://github.com/syohex/emacs-zoom-window][zoom-window]]
#+begin_src emacs-lisp :tangle no
(use-package zoom-window
  :general
  (d/leader-keys
    "wz" 'zoom-window-zoom)
  :config
  (setq zoom-window-mode-line-color "#1D2021"))
#+end_src
** Hydras
#+begin_src emacs-lisp
(defhydra d/splitter ()
  "
╭────────╮
│ window │
└────────┴────────────────
 [_>_] → [_+_] ↑ [_w_] next [_r_] ↻
 [_<_] ← [_-_] ↓ [_W_] prev [_R_] ↺
────────────────────────────
"
  (">" evil-window-increase-width)
  ("<" evil-window-decrease-width)
  ("+" evil-window-increase-height)
  ("-" evil-window-descrease-height)
  ("r" evil-window-rotate-downwards)
  ("R" evil-window-rotate-upwards)
  ("w" evil-window-next)
  ("W" evil-window-prev))
#+end_src
** Bindings
#+begin_src emacs-lisp
(d/leader-keys
  "jI" 'imenu-anywhere
  "jf" 'find-function-other-window
  "ji" 'imenu
  "jv" 'find-variable-other-window
  "jj" 'find-library-other-window

  ;; "wd" 'delete-window
  "wd" (lambda ()
         (interactive)
         (message "Use 'C-w c' to delete window."))
  ;; "wf" 'make-frame
  "wf" (lambda ()
         (interactive)
         (message "Use 'C-w f' to make a new frame."))
  ;; "wh" 'd/split-horz-focus
  "wh" (lambda ()
         (interactive)
         (message "Use 'C-w s' to split window horizontally."))
  ;; "wo" 'delete-other-windows
  "wo" (lambda ()
         (interactive)
         (message "Use 'C-w o' to delete other windows."))
  "ws" (lambda ()
         (interactive)
         (message "Use C-w ... directly to access window manipulation."))
  "wt" 'd/toggle-window-split
  ;; "wv" 'd/split-vert-focus
  "wv" (lambda ()
         (interactive)
         (message "Use 'C-w v' to split windows vertically.")))

(general-def evil-window-map
  ">" 'd/splitter/evil-window-increase-width
  "<" 'd/splitter/evil-window-decrease-width
  "+" 'd/splitter/evil-window-increase-height
  "-" 'd/splitter/evil-window-decrease-height
  "w" 'd/splitter/evil-window-next
  "W" 'd/splitter/evil-window-prev
  "r" 'd/splitter/evil-window-rotate-downwards
  "R" 'd/splitter/evil-window-rotate-upwards
  "f" 'make-frame)
#+end_src
* Appearance
** Defaults
*** Startup
Get right to the scratch buffer if not editing a file.
#+begin_src emacs-lisp
(setq inhibit-startup-screen t
      inhibit-startup-echo-area-message t)
#+end_src
*** Time display
#+begin_src emacs-lisp :tangle no
(d/after 'time
  (setq  display-time-24hr-format t
         display-time-default-load-average nil
         display-time-format "│ %Y-%d-%m %H:%M │"
         display-time-load-average nil))
#+end_src
*** Battery
#+begin_src emacs-lisp :tangle no
(d/after 'battery
  (setq battery-mode-line-format "%p" ; only percent
        battery-mode-line-limit 101)  ; show battery at 100%

  (define-advice battery-linux-sysfs (:around (fn) d/fix-percent)
    "Don't report >100% battery, hide decimal percent."
    (let* ((alist (funcall fn))
           (bat (string-to-number (alist-get 112 alist))))
      (setf (alist-get 112 alist) (if (>= bat 100)
                                      "100"
                                    (format "%.f" bat)))
      alist)))
#+end_src

*** Gui elements
Turn off gui elements that I never use. Gui emacs is great, but I still prefer
text-based interaction thank you very much.
#+begin_src emacs-lisp
(setq custom-raised-buttons nil
      use-dialog-box nil)
#+end_src
*** Buffer display
I don't want line-wrapping madness, just tell me there's more to see and I'll
have a look. Additionally, show whitespace.
#+begin_src emacs-lisp
(setq-default truncate-lines t)
#+end_src
*** Font
Set the font to Iosevka Term, if available.
#+begin_src emacs-lisp
(when (and (display-graphic-p) (x-list-fonts "Iosevka Term"))
  (add-to-list 'default-frame-alist '(font . "Iosevka Term-10:weight=book"))
  (set-face-attribute 'default t :font "Iosevka Term-10:weight=book"))
#+end_src
*** Minibuffer
Use a bar cursor in the minibuffer.
#+begin_src emacs-lisp
(d/hook! minibuffer-setup
  (setq-local cursor-type '(bar . 1)))
#+end_src
*** Margin
#+begin_src emacs-lisp :tangle no
(setq-default left-margin-width 1)
(setq-default right-margin-width 1)
#+end_src
*** Themes
#+begin_src emacs-lisp
(setq custom-safe-themes t)
#+end_src
*** variable-pitch
I don't like seeing variable pitch in emacs. Just sorta throws me off.
#+begin_src emacs-lisp
(face-spec-set 'variable-pitch '((t nil)))
#+end_src
** Built-in
*** [[elisp:(find-library-other-window%20"hl-line")][hl-line]]
#+begin_src emacs-lisp
(use-package hl-line
  :straight nil
  :hook ((prog-mode
          emms-browser-mode
          dired-mode
          conf-mode)
         . hl-line-mode))
#+end_src
*** whitespace
#+begin_src emacs-lisp
(use-package whitespace
  :defer 5
  :config
  (setq whitespace-style '(face trailing))
  (setq whitespace-global-modes '(not erc-mode ses-mode))
  (global-whitespace-mode))
#+end_src
** Packages
*** [[https://github.com/larstvei/Focus][focus]]
#+begin_quote
Dim the font color of text in surrounding paragraphs
#+end_quote
#+begin_src emacs-lisp
(use-package focus
  :custom
  (focus-mode-to-thing
   '((prog-mode . defun)
     (text-mode . sentence)
     (org-mode . paragraph))))
#+end_src
*** [[http://git.savannah.gnu.org/cgit/emacs/elpa.git/tree/packages/rainbow-mode/rainbow-mode.el][rainbow-mode]]
#+begin_quote
Colorize color names in buffers
#+end_quote
#+begin_src emacs-lisp
(use-package rainbow-mode
  :hook help-mode
  :init
  (d/safe-vars! (eval when (fboundp 'rainbow-mode) (rainbow-mode 1)))
  :config
  (setq rainbow-x-colors-major-mode-list '(c-mode c++-mode java-mode)))
#+end_src
*** [[https://github.com/dieggsy/emacs-theme-darktooth][darktooth-theme]]
#+begin_quote
An Emacs 24 theme remixed from gruvbox
#+end_quote
(my fork)
#+begin_src emacs-lisp
(use-package darktooth-theme
  :demand t
  :straight (:host github
             :repo "dieggsy/emacs-theme-darktooth"
             :fork (:host github
                    :repo "emacsfodder/emacs-theme-darktooth"))
  :config
  (load-theme 'darktooth))
#+end_src
*** [[https://github.com/jordonbiondo/column-enforce-mode][column-enforce-mode]]
#+begin_quote
Highlight text that extends beyond a certain column.
#+end_quote
#+begin_src emacs-lisp
(use-package column-enforce-mode
  :hook (prog-mode conf-mode)
  :init
  (d/safe-vars! column-enforce-mode)
  :custom
  (column-enforce-column 79))
#+end_src
*** [[https://github.com/gonewest818/dimmer.el][dimmer]]
#+begin_src emacs-lisp
(use-package dimmer)
#+end_src
*** [[https://github.com/dieggsy/eterm-256color][eterm-256color]]
#+begin_quote
Customizable 256 colors for emacs term and ansi-term
#+end_quote
#+begin_src emacs-lisp
(use-package eterm-256color
  :straight (:host github
             :branch "devel"
             :repo "dieggsy/eterm-256color"
             :files ("eterm-256color.el" "eterm-256color.ti"))
  :hook (term-mode . eterm-256color-mode))
#+end_src
*** [[https://github.com/ankurdave/color-identifiers-mode][color-identifiers]]
#+begin_quote
Emacs minor mode to highlight each source code identifier uniquely based on its name
#+end_quote
#+begin_src emacs-lisp :tangle no
(use-package color-identifiers-mode)
#+end_src
*** [[https://github.com/Fanael/highlight-numbers][highlight-numbers]]
#+begin_quote
Highlight numbers in source code
#+end_quote
Neat-o
#+begin_src emacs-lisp
(use-package highlight-numbers
  :hook (((prog-mode conf-mode) . highlight-numbers-mode)
         (json-mode . highlight-numbers--turn-off)))
#+end_src
*** [[https://github.com/tsdh/highlight-parentheses.el][highlight-parentheses]]
#+begin_quote
Emacs: highlight surrounding parentheses
#+end_quote
#+begin_src emacs-lisp
(use-package highlight-parentheses
  :hook ((prog-mode conf-mode) . highlight-parentheses-mode))
#+end_src
*** [[https://github.com/ecraven/no-emoji/][no-emoji]]
#+begin_src emacs-lisp
(use-package no-emoji
  :hook (erc-mode . no-emoji-minor-mode))
#+end_src
*** [[https://github.com/milkypostman/powerline][powerline]]
#+begin_src emacs-lisp
(use-package powerline)
#+end_src
*** [[https://github.com/Fanael/rainbow-delimiters][rainbow-delimiters]]
Better parentheses coloring
#+begin_src emacs-lisp
(use-package rainbow-delimiters
  :hook ((prog-mode
          conf-mode
          geiser-repl-mode
          ielm-mode)
         .
         rainbow-delimiters-mode))
#+end_src
*** [[https://github.com/atomontage/xterm-color][xterm-color]]
#+begin_quote
ANSI & xterm-256 color text property translator for Emacs
#+end_quote
#+begin_src emacs-lisp
(use-package xterm-color
  :commands xterm-color-filter
  :hook ((eshell-before-prompt . d/xterm-color-preserve-properties))
  :config
  (defun d/xterm-color-preserve-properties ()
    (setq xterm-color-preserve-properties t)))
#+end_src
** Hydras
#+begin_src emacs-lisp
(defhydra d/zoom ()
  "
╭──────╮
│ zoom │
└──────┴──────────────────
 [_=_] in [_-_] out [_0_] reset
──────────────────────────
"
  ("=" text-scale-increase)
  ("-" text-scale-decrease)
  ("0" (text-scale-adjust 0)))
#+end_src
** Bindings
#+begin_src emacs-lisp
(d/leader-keys
  "z" '(:def d/zoom/body :wk "zoom"))
#+end_src
** Mode line
*** Helpers
#+begin_src emacs-lisp
(defun d/flycheck-lighter (state)
  "Return flycheck information for the given error type STATE.

Source: https://git.io/vQKzv"
  (let* ((counts (flycheck-count-errors flycheck-current-errors))
         (errorp (flycheck-has-current-errors-p state))
         (err (or (cdr (assq state counts)) "?"))
         (running (eq 'running flycheck-last-status-change)))
    (if (or errorp running) (format "•%s" err))))

(defmacro d/with-window-status (&rest body)
  (declare (indent defun))
  `(let ((window-active? (powerline-selected-window-active)))
     (cl-flet ((propertize-active
                (&rest arguments)
                (let ((str (car arguments)))
                  (cond  ((and window-active? (stringp str))
                          (apply #'propertize arguments) )
                         ((stringp str)
                          (car arguments))))))
       ,@body)))

(defmacro d/with-evil-tag-color (&rest body)
  (declare (indent defun))
  `(let ((evil-tag-color
          (pcase (substring-no-properties
                  evil-mode-line-tag)
            (" N " "#B8BB26")
            (" I " "#66999D")
            (" M " "#D3869B")
            (" V " "#FE8019")
            (" E " "#FABD2F" )
            (" R " "#FE8019")
            (" O " "#B8BB26"))))
     ,@body))

(defun d/make-xpm (color height width)
  "Create an XPM bitmap.

Source: https://git.io/vQKzL"
  (if (display-graphic-p)
      (propertize
       " " 'display
       (let ((data (make-list height (make-list width 1)))
             (color (or color "None"))
             (xpm-name (intern (concat "d/ml-xpm-" (substring color 1)))))
         (if (boundp xpm-name)
             (symbol-value xpm-name)
           (set xpm-name
                (create-image
                 (concat
                  (format "/* XPM */\nstatic char * percent[] = {\n\"%i %i 2 1\",\n\". c %s\",\n\"  c %s\","
                          (length (car data))
                          (length data)
                          color
                          color)
                  (apply #'concat
                         (cl-loop with idx = 0
                                  with len = (length data)
                                  for dl in data
                                  do (cl-incf idx)
                                  collect
                                  (concat "\""
                                          (cl-loop for d in dl
                                                   if (= d 0) collect (string-to-char " ")
                                                   else collect (string-to-char "."))
                                          (if (eq idx len) "\"};" "\",\n")))))
                 'xpm t :ascent 'center)))))
    (propertize
     " " 'face
     `(:background ,color))))

(defun d/eyebrowse-relevant? ()
  (and (featurep 'eyebrowse)
       (< 1 (length (eyebrowse--get 'window-configs)))))

(defun d/in-macro? ()
  (or defining-kbd-macro executing-kbd-macro))

(defun d/in-evil-substitution? ()
  (or (assq 'evil-ex-substitute evil-ex-active-highlights-alist)
      (assq 'evil-ex-global-match evil-ex-active-highlights-alist)
      (assq 'evil-ex-buffer-match evil-ex-active-highlights-alist)))

(defun d/evil-substitute-num-matches ()
  "Return the number of matches for the current evil substitution.

Source: https://git.io/vQKzq"
  (let ((range (if evil-ex-range
                   (cons (car evil-ex-range) (cadr evil-ex-range))
                 (cons (line-beginning-position) (line-end-position))))
        (pattern (car-safe (evil-delimited-arguments evil-ex-argument 2))))
    (if pattern
        (format "%s matches" (how-many pattern (car range) (cdr range)))
      " ... ")))
#+end_src
*** Mode-line
#+begin_src emacs-lisp
(defvar d/mode-line
  '((:eval
     (d/with-evil-tag-color
       (d/with-window-status
         (cond ((and window-active?
                     (not (or (d/eyebrowse-relevant?)
                              (bound-and-true-p anzu--state)
                              (d/in-macro?)
                              (d/in-evil-substitution?))))
                (d/make-xpm evil-tag-color 47 8))
               ((not window-active?)
                (d/make-xpm "#1D2021" 47 8))))))
    (:eval
     (d/with-window-status
       (d/with-evil-tag-color
         (when window-active?
           (propertize-active
            (cond ((d/in-evil-substitution?)
                   (d/evil-substitute-num-matches))
                  ((d/in-macro?)
                   (if (bound-and-true-p evil-this-macro)
                       (char-to-string evil-this-macro)
                     "Macro"))
                  ((bound-and-true-p anzu--state)
                   (anzu--update-mode-line))
                  ((d/eyebrowse-relevant?)
                   (let* ((num (eyebrowse--get 'current-slot))
                          (tag (nth 2 (assoc num (eyebrowse--get 'window-configs)))))
                     (if (and tag (< 0 (length tag)))
                         tag
                       (int-to-string num)))))
            'face
            `(:foreground
              "#3E3D31"
              :weight bold
              :background ,evil-tag-color
              :box (:color ,evil-tag-color :line-width 9)))))))
    ;; File modified
    " %* "
    ;; Buffer name & recursive editing
    "%[" "%b" "%]  "
    ;; Remote
    (:eval (d/with-window-status
             (let ((host (file-remote-p default-directory 'host)))
               (propertize-active
                (and host
                     (format "%s@%s "
                             (file-remote-p default-directory 'user)
                             host))
                'face
                '(:foreground "#D3869B")))))
    ;; Line/column number
    ;; (:eval (d/with-window-status
    ;;          (propertize-active "%4l:%2c  "
    ;;                             'face '(:foreground "#A89984"))))
    ;; Major mode
    (:eval
     (d/with-window-status
       (unless (file-name-extension (buffer-name))
         (propertize-active
          (concat mode-name "  ")
          'face '(:foreground "#83A598")))))
    ;; Version control
    (:eval
     (d/with-window-status
       (when vc-mode
         (propertize-active
          (concat (replace-regexp-in-string "^ Git." "" vc-mode) "  ")
          'face '(:foreground "#DD6F48" )))))
    ;; Flycheck
    (:eval
     (d/with-window-status
       (when (and (bound-and-true-p flycheck-mode)
                  (or flycheck-current-errors
                      (eq 'running flycheck-last-status-change)))
         (concat
          (cl-loop for state in '((error . "#FB4933")
                                  (warning . "#FABD2F")
                                  (info . "#83A598"))
                   as lighter = (d/flycheck-lighter (car state))
                   when lighter
                   concat (propertize-active
                           lighter
                           'face `(:foreground ,(cdr state))))
          " "))))
    ;; Input method
    (:eval
     (d/with-window-status
       (when (or current-input-method
                 (and (bound-and-true-p evil-mode)
                      (bound-and-true-p evil-input-method)))
         (cond
          (current-input-method
           (propertize-active
            (concat current-input-method-title " ")
            'face
            'bold))
          ((and (featurep 'evil) (bound-and-true-p evil-input-method))
           (concat (nth 3 (assoc default-input-method input-method-alist))
                   " "))))))
    erc-modified-channels-object))

(setq-default mode-line-format d/mode-line)
(with-current-buffer "*Messages*"
  (setq-local mode-line-format d/mode-line))
#+end_src
* Dev
** Prog
*** Packages
**** [[https://github.com/Malabarba/aggressive-indent-mode/][aggressive-indent]]
#+begin_quote
Emacs minor mode that keeps your code always indented. More reliable than electric-indent-mode.
#+end_quote
#+begin_src emacs-lisp
(use-package aggressive-indent
  :hook ((lisp-mode
          emacs-lisp-mode
          scheme-mode
          c-mode
          c++-mode)
         . aggressive-indent-mode))
#+end_src
**** [[https://github.com/flycheck/flycheck][flycheck]]
#+begin_quote
On the fly syntax checking for GNU Emacs
#+end_quote
#+begin_src emacs-lisp
(use-package flycheck
  :hook (python-mode . flycheck-mode)
  :general
  (d/mode-leader-keys prog-mode-map
    "f" '(:def d/flycheck/body :wk "flycheck"))
  :config
  (setq flycheck-completing-read-function 'ivy-completing-read)
  (define-fringe-bitmap 'flycheck-fringe-bitmap-double-arrow
    [#b10000000
     #b01000000
     #b11100000
     #b00010000
     #b11111000
     #b00000100
     #b11111110
     #b00000100
     #b11111000
     #b00010000
     #b11100000
     #b01000000
     #b10000000])

  (defhydra d/flycheck
    (:body-pre (progn
                 (flycheck-mode 1)
                 (flycheck-list-errors))
     :post (quit-windows-on "*Flycheck errors*"))
    "
╭──────────╮
│ flycheck │
└──────────┴────────
 [_>_] next [_H_] first
 [_<_] prev [_L_] last
────────────────────
"
    ("f"  flycheck-error-list-set-filter)
    (">"  flycheck-next-error)
    ("<"  flycheck-previous-error)
    ("H" flycheck-first-error)
    ("L"  (progn (goto-char (point-max))
                 (flycheck-previous-error)))))
#+end_src
*** Setup
#+begin_src emacs-lisp
(d/hook! prog-mode
  (subword-mode)
  (auto-fill-mode 1)
  (setq-local comment-auto-fill-only-comments t))
#+end_src
** Lang
*** C++
**** Packages
***** irony
#+begin_src emacs-lisp :tangle no
(use-package irony
  :hook ((c++-mode . irony-mode)
         (irony-mode . irony-cdb-autosetup-compile-options)))
#+end_src
***** company-irony
#+begin_src emacs-lisp :tangle no
(use-package company-irony
  :after irony
  :demand
  :init
  (d/after 'company
    '(add-to-list 'company-backends 'company-irony)))
#+end_src
***** lsp-mode
#+begin_src emacs-lisp
(use-package lsp-mode)
#+end_src
***** cquery
#+begin_src emacs-lisp :tangle no
(use-package cquery
  :hook (c++-mode . lsp-cquery-enable)
  :config
  (setq cquery-executable "/usr/bin/cquery"))
#+end_src
***** ccls
#+begin_src emacs-lisp
(use-package ccls
  :hook (c++-mode . lsp-ccls-enable))
#+end_src
***** lsp-ui
#+begin_src emacs-lisp
(use-package lsp-ui
  :hook (lsp-mode . lsp-ui-mode))
#+end_src
***** company-lsp
#+begin_src emacs-lisp
(use-package company-lsp
  :after lsp-mode
  :demand t
  :config
  (push '(company-lsp :with company-yasnippet) company-backends))
#+end_src
**** Setup
#+begin_src emacs-lisp
(defalias 'cpp-mode 'c++-mode)
(d/after 'cc-mode
  (setq-default c-basic-offset 4)
  (c-set-offset 'case-label '+))
#+end_src
*** Python
**** Packages
***** [[https://github.com/proofit404/anaconda-mode][anaconda-mode]]
#+begin_quote
Code navigation, documentation lookup and completion for Python.
#+end_quote
#+begin_src emacs-lisp
(use-package anaconda-mode
  :hook (python-mode
         (python-mode . anaconda-eldoc-mode))
  :config
  (d/after 'company
    (add-to-list 'company-backends
                 (company-mode/backend-with-yas 'company-anaconda)) ))
#+end_src
***** [[https://github.com/proofit404/company-anaconda][company-anaconda]]
#+begin_quote
Anaconda backend for company-mode.
#+end_quote
#+begin_src emacs-lisp
(use-package company-anaconda
  :after (python company)
  :demand t)
#+end_src
***** [[https://github.com/millejoh/emacs-ipython-notebook][EIN]]
#+begin_quote
Jupyter and IPython 2.x/3.x notebook client in Emacs
#+end_quote
#+begin_src emacs-lisp :tangle no
(use-package ein)
#+end_src
***** [[https://github.com/proofit404/pyenv-mode][pyenv-mode]]
#+begin_quote
Integrate pyenv with python-mode.
#+end_quote
#+begin_src emacs-lisp
(use-package pyenv-mode
  :config
  (pyenv-mode))
#+end_src
***** [[https://github.com/ssbb/pyenv-mode-auto][pyenv-mode-auto]]
#+begin_quote
Automatically activates pyenv version within Emacs if .python-version file exists.
#+end_quote
#+begin_src emacs-lisp
(use-package pyenv-mode-auto
  :after pyenv-mode
  :demand t)
#+end_src

***** [[https://github.com/naiquevin/sphinx-doc.el][sphinx-doc]]
#+begin_quote
Generate Sphinx friendly docstrings for Python functions in Emacs
#+end_quote
Pretty neat, though not entirely complete, IMO.
#+begin_src emacs-lisp :tangle no
(use-package sphinx-doc)
#+end_src

***** [[https://github.com/JorisE/yapfify][yapfify]]
#+begin_quote
Yapf for Emacs
#+end_quote
#+begin_src emacs-lisp
(use-package yapfify)
#+end_src
**** Setup
#+begin_src emacs-lisp
(d/hook! python-mode
  (set (make-local-variable 'comment-inline-offset) 2))

(d/after 'python
  (setq python-shell-completion-native-enable nil))
#+end_src
*** Hy
#+begin_src emacs-lisp :tangle no
(use-package hy-mode)
#+end_src
*** Lisps
**** Packages
***** [[https://github.com/slime/slime][slime]]
#+begin_quote
The Superior Lisp Interaction Mode for Emacs
#+end_quote
#+begin_src emacs-lisp :tangle no
(use-package slime
  :general
  (imap slime-repl-mode-map
    [up] 'slime-repl-previous-input
    [down] 'slime-repl-next-input)
  :config
  (require 'slime-macrostep)
  (setq inferior-lisp-program (executable-find "sbcl")
        slime-contribs '(slime-fancy slime-company)))
#+end_src
***** [[https://github.com/anwyn/slime-company][slime-company]]
#+begin_src emacs-lisp :tangle no
(use-package slime-company
  :after slime
  :demand t)
#+end_src
***** [[https://github.com/tkf/emacs-request][request]]
#+begin_quote
Easy HTTP request for Emacs Lisp
#+end_quote
#+begin_src emacs-lisp
(use-package request)

(use-package request-deferred)
#+end_src
***** [[https://github.com/magnars/s.el][s]]
#+begin_quote
The long lost Emacs string manipulation library.
#+end_quote
#+begin_src emacs-lisp
(use-package s)
#+end_src
***** [[https://github.com/magnars/dash.el][dash]]
#+begin_quote
A modern list library for Emacs
#+end_quote
#+begin_src emacs-lisp
(use-package dash)
#+end_src
***** [[https://github.com/rejeep/f.el][f]]
#+begin_quote
Modern API for working with files and directories in Emacs
#+end_quote
#+begin_src emacs-lisp
(use-package f)
#+end_src
***** [[https://github.com/purcell/flycheck-package][flycheck-package]]
#+begin_quote
Flycheck checker for elisp package metadata
#+end_quote
#+begin_src emacs-lisp
(use-package flycheck-package
  :after flycheck
  :demand t
  :config
  (flycheck-package-setup))
#+end_src
***** [[https://github.com/Wilfred/suggest.el][suggest]]
#+begin_quote
discover elisp functions that do what you want
#+end_quote
#+begin_src emacs-lisp :tangle no
(use-package suggest)
#+end_src
***** [[https://github.com/dieggsy/difflib.el][difflib.el]]
#+begin_quote
Port of python's difflib to emacs-lisp
#+end_quote
#+begin_src emacs-lisp
(use-package difflib
  :straight (:host github
             :repo "dieggsy/difflib.el"))
#+end_src
***** [[https://github.com/joddie/macrostep][macrostep]]
#+begin_quote
interactive macro-expander
#+end_quote
#+begin_src emacs-lisp
(use-package macrostep
  :general
  (d/mode-leader-keys (emacs-lisp-mode-map lisp-mode-map)
    "m" 'macrostep-expand))
#+end_src
**** Functions
#+begin_src emacs-lisp
(defun d/eval-surrounding-sexp (levels)
  "Eval sexp around point, specifying depth with LEVELS.

Source: http://tinyurl.com/le6wxuo"
  (interactive "p")
  (save-excursion
    (sp-end-of-sexp (abs levels))
    (eval-last-sexp nil)))

(defmacro d/let (&rest clause)
  (declare (indent defun))
  (pcase clause
    ((and `(,name ,vars . ,body) (guard (symbolp name)))
     (let ((args (cl-loop for i in vars if (listp i) collect (car i) else collect i))
           (init-vals (cl-loop for i in vars if (listp i) append (cdr i) else collect nil)))
       `(cl-labels ((,name ,args ,@body))
          (,name ,@init-vals))))
    (_ `(let ,@clause))))

(defun d/lisp-indent-function (indent-point state)
  "This function is the normal value of the variable `lisp-indent-function'.
The function `calculate-lisp-indent' calls this to determine
if the arguments of a Lisp function call should be indented specially.
INDENT-POINT is the position at which the line being indented begins.
Point is located at the point to indent under (for default indentation);
STATE is the `parse-partial-sexp' state for that position.
If the current line is in a call to a Lisp function that has a non-nil
property `lisp-indent-function' (or the deprecated `lisp-indent-hook'),
it specifies how to indent.  The property value can be:
,* `defun', meaning indent `defun'-style
  \(this is also the case if there is no property and the function
  has a name that begins with \"def\", and three or more arguments);
,* an integer N, meaning indent the first N arguments specially
  (like ordinary function arguments), and then indent any further
  arguments like a body;
,* a function to call that returns the indentation (or nil).
  `lisp-indent-function' calls this function with the same two arguments
  that it itself received.
This function returns either the indentation to use, or nil if the
Lisp function does not specify a special indentation.

Source: https://git.io/vQKz8"
  (let ((normal-indent (current-column))
        (orig-point (point)))
    (goto-char (1+ (elt state 1)))
    (parse-partial-sexp (point) calculate-lisp-indent-last-sexp 0 t)
    (cond
     ;; car of form doesn't seem to be a symbol, or is a keyword
     ((and (elt state 2)
           (or (not (looking-at "\\sw\\|\\s_"))
               (looking-at ":")))
      (if (not (> (save-excursion (forward-line 1) (point))
                  calculate-lisp-indent-last-sexp))
          (progn (goto-char calculate-lisp-indent-last-sexp)
                 (beginning-of-line)
                 (parse-partial-sexp (point)
                                     calculate-lisp-indent-last-sexp 0 t)))
      ;; Indent under the list or under the first sexp on the same
      ;; line as calculate-lisp-indent-last-sexp.  Note that first
      ;; thing on that line has to be complete sexp since we are
      ;; inside the innermost containing sexp.
      (backward-prefix-chars)
      (current-column))
     ((and (save-excursion
             (goto-char indent-point)
             (skip-syntax-forward " ")
             (not (looking-at ":")))
           (save-excursion
             (goto-char orig-point)
             (looking-at ":")))
      (save-excursion
        (goto-char (+ 2 (elt state 1)))
        (current-column)))
     (t
      (let ((function (buffer-substring (point)
                                        (progn (forward-sexp 1) (point))))
            method)
        (setq method (or (function-get (intern-soft function)
                                       'lisp-indent-function)
                         (get (intern-soft function) 'lisp-indent-hook)))
        (cond ((or (eq method 'defun)
                   (and (null method)
                        (> (length function) 3)
                        (string-match "\\`def" function)))
               (lisp-indent-defform state indent-point))
              ((integerp method)
               (lisp-indent-specform method state
                                     indent-point normal-indent))
              (method
               (funcall method indent-point state))))))))
#+end_src
**** Bindings
#+begin_src emacs-lisp
(d/mode-leader-keys emacs-lisp-mode-map
  "eb" 'eval-buffer
  "ef" 'eval-defun
  "er" 'eval-region
  "es" 'd/eval-surrounding-sexp
  "el" 'eval-last-sexp)
 #+end_src
**** Setup
#+begin_src emacs-lisp
(d/hook! (lisp-mode emacs-lisp-mode)
  (hs-minor-mode)
  (d/after 'evil-surround
    (push '(? . ("`" . "'")) evil-surround-pairs-alist))
  (setq-local lisp-indent-function #'d/lisp-indent-function))
#+end_src
*** Haskell
**** Packages
***** [[https://github.com/haskell/haskell-mode][haskell-mode]]
#+begin_src emacs-lisp :tangle no
(use-package haskell-mode
  :mode "\\.hs\\'"
  :config
  (setq haskell-indentation-layout-offset 4
        haskell-indentation-left-offset 4
        haskell-indentation-ifte-offset 4))

#+end_src
*** Scheme
**** Packages
***** [[https://github.com/jaor/geiser][geiser]]
#+begin_quote
emacs and scheme talk to each other
#+end_quote
#+begin_src emacs-lisp
(use-package geiser
  :custom
  (geiser-chicken-binary '("csi" "-q"))
  (geiser-chicken-required-modules nil)
  :config
  (setq geiser-active-implementations '(racket chicken)
        geiser-default-implementation 'racket
        geiser-debug-jump-to-debug-p nil
        geiser-debug-show-debug-p nil
        geiser-chicken-load-init-file-p t))
#+end_src
**** Functions
#+begin_src emacs-lisp
(defun d/geiser-eval-surrounding-sexp (levels)
  "Eval sexp around point, specifying depth with LEVELS.

Source: http://tinyurl.com/le6wxuo"
  (interactive "p")
  (save-excursion
    (sp-end-of-sexp (abs levels))
    (geiser-eval-last-sexp nil)))
#+end_src
**** Bindings
#+begin_src emacs-lisp
(d/mode-leader-keys scheme-mode-map
  "eb" 'geiser-eval-buffer
  "ef" 'geiser-eval-definition
  "er" 'geiser-eval-region
  "el" 'geiser-eval-last-sexp
  "es" 'd/geiser-eval-surrounding-sexp)
#+end_src
**** Setup
#+begin_src emacs-lisp
(d/hook! scheme-mode
  (hs-minor-mode)
  (d/after 'evil-surround
    (push '(? . ("`" . "'")) evil-surround-pairs-alist)))

(d/after 'scheme
  (put 'set-sharp-read-syntax! 'scheme-indent-function 1)
  (put 'set-read-syntax! 'scheme-indent-function 1))

(font-lock-add-keywords
 'scheme-mode
 '(("\\<\\sw+:\\>" . 'font-lock-builtin-face)
   ("\\`\\(#!/\\(?:\\(?:.*?/\\)+\\)?\\([^/ ]+\\)?\\(?:.*$\\)?\\)"
    (1 'font-lock-comment-face)
    (2 'font-lock-keyword-face t))
   ("\\<#!\\sw+\\>" . 'font-lock-type-face))
 t)
(font-lock-add-keywords
 'geiser-repl-mode
 '(("\\<\\sw+:\\>" . 'font-lock-builtin-face)
   ("\\<#!\\sw+\\>" . 'font-lock-type-face))
 t)
#+end_src
*** [[https://github.com/NixOS/nix-mode][nix-mode]]
#+begin_quote
An Emacs major mode for editing Nix expressions.
#+end_quote
#+begin_src emacs-lisp
(use-package nix-mode
  :commands (d/nix-update-fetchgit d/nix-update-my-packages)
  :config
  (require 'hilit-chg)

  (defmacro d/nix-update-helper (search get-url)
    `(save-excursion
       (goto-char (point-min))
       (d/let lop ((bound
                    (re-search-forward
                     (rx "=" (opt " ") ,search (opt " ") "{"
                         (group (minimal-match (0+ anything))) "}")
                     nil
                     'noerror)))
         (if (not bound)
             '()
           (cons
            (progn
              (goto-char (match-beginning 1))
              (list
               (save-excursion
                 ,get-url)
               (save-excursion
                 (and
                  (re-search-forward "\"?rev\"?[ ]*?=[ ]*?\"\\(.*\\)\""
                                     bound)
                  (list (match-beginning 1)
                        (match-end 1))))
               (save-excursion
                 (and
                  (re-search-forward "\"?sha256\"?[ ]*?=[ ]*?\"\\(.*\\)\""
                                     bound)
                  (list (match-beginning 1)
                        (match-end 1))))))
            (lop (re-search-forward
                  (rx "=" (opt " ") ,search (opt " ") "{"
                      (group (minimal-match (0+ anything))) "}")
                  nil
                  'noerror)))))))

  (cl-defun d/nix-update-fetchgit (&optional (file (buffer-file-name)))
    (interactive)
    (let ((visited-p (get-file-buffer file))
          (file-buffer (find-file-noselect file)))
      (with-current-buffer file-buffer
        (let* ((git-urls
                (d/nix-update-helper
                 "fetchgit"
                 (and
                  (re-search-forward "\"?url\"?[ ]*?=[ ]*?\"\\(.*\\)\"" bound)
                  (match-string-no-properties 1))))
               (github-urls
                (d/nix-update-helper
                 "fetchFromGitHub"
                 (concat
                  "https://github.com/"
                  (and
                   (save-excursion
                     (re-search-forward "\"?owner\"?[ ]*?=[ ]*?\"\\(.*\\)\"" bound)
                     (match-string-no-properties 1)))
                  "/"
                  (and
                   (save-excursion
                     (re-search-forward "\"?repo\"?[ ]*?=[ ]*?\"\\(.*\\)\"" bound)
                     (match-string-no-properties 1))))))
               (all-urls (append git-urls github-urls)))
          (dolist (repo all-urls)
            (let ((url (car repo))
                  (rev-bounds (cadr repo))
                  (sha-bounds (caddr repo)))
              (make-process
               :name (format "nix-prefetch-git: %s" url)
               :command `("nix-prefetch-git" ,url "--fetch-submodules")
               :stderr (get-buffer-create "*nix-prefetch-stderr*")
               :filter
               `(lambda (process output)
                  (let* ((json-vals (json-read-from-string output))
                         (rev (alist-get 'rev json-vals))
                         (sha (alist-get 'sha256 json-vals)))
                    (let ((visited-p (get-file-buffer ,file))
                          (file-buffer (find-file-noselect ,file)))
                      (with-current-buffer file-buffer
                        (unless highlight-changes-mode
                          (highlight-changes-mode))
                        (save-excursion
                          (unless (string=
                                   (apply #'buffer-substring-no-properties
                                          ',rev-bounds)
                                   rev)
                            (apply #'delete-region ',rev-bounds)
                            (goto-char (car ',rev-bounds))
                            (insert rev))
                          (unless (string=
                                   (apply #'buffer-substring-no-properties
                                          ',sha-bounds)
                                   sha)
                            (apply #'delete-region ',sha-bounds)
                            (goto-char (car ',sha-bounds))
                            (insert sha)))
                        (save-buffer))
                      (unless visited-p
                        (kill-buffer file-buffer)))))))))
        (unless visited-p
          (kill-buffer file-buffer)))))

  (defun d/nix-update-my-packages ()
    (interactive)
    (let ((nix-files
           (split-string
            (shell-command-to-string
             "grep -lrP 'fetchgit|fetchFromGitHub' ~/dotfiles/nix-local")
            "\n"
            'omit-nulls)))
      (dolist (file nix-files)
        (d/nix-update-fetchgit file)))))
#+end_src
*** [[https://github.com/mcandre/vimrc-mode][vimrc-mode]]
#+begin_quote
Enables syntax highlighting for .vimrc/_vimrc files
#+end_quote
#+begin_src emacs-lisp
(use-package vimrc-mode)
#+end_src
*** [[https://github.com/fxbois/web-mode][web-mode]]
#+begin_quote
web template editing mode for emacs
#+end_quote
#+begin_src emacs-lisp
(use-package web-mode
  :mode ("\\.html?\\'" "\\.xml\\'" "\\.launch\\'")
  :config
  (setq web-mode-markup-indent-offset 2))
#+end_src
*** [[https://github.com/wwwjfy/emacs-fish][fish-mode]]
#+begin_src emacs-lisp
(use-package fish-mode)
#+end_src
** VCS
*** Functions
#+begin_src emacs-lisp
(defun d/magit-blame-toggle ()
  "Toggle magit-blame-mode on and off interactively.

Source: https://git.io/vQKub"
  (interactive)
  (if (bound-and-true-p magit-blame-mode)
      (magit-blame-quit)
    (call-interactively 'magit-blame)))

(defun d/gitlab-snippet (&optional private)
  (interactive "P")
  (require 'request)
  (let* ((buf-name (buffer-name))
         (title (read-from-minibuffer "Snippet title: "))
         (file-name (read-from-minibuffer (format "File name [%s]: " buf-name)
                                          nil
                                          nil
                                          nil
                                          nil
                                          buf-name))
         (content (if (region-active-p)
                      (buffer-substring (region-beginning) (region-end))
                    (buffer-string)))
         (visibility (if private "private" "public"))
         (token (password-store-get "tokens/gitlab/master")))
    (request
     "https://gitlab.com/api/v4/snippets"
     :type "POST"
     :data `(("title" . ,title)
             ("file_name" . ,file-name)
             ("content" . ,content)
             ("visibility" . ,visibility))
     :success (cl-function (lambda (&key data &allow-other-keys)
                             (let ((url (gethash "web_url" data)))
                               (kill-new url)
                               (message "Added %S to kill ring" url))))
     :headers `(("PRIVATE-TOKEN" . ,token))
     :parser 'json-parse-buffer)))
#+end_src
*** Packages
**** [[https://github.com/defunkt/gist.el][gist]]
#+begin_quote
Yet another Emacs paste mode, this one for Gist.
#+end_quote
#+begin_src emacs-lisp :tangle no
(use-package gist
  :config
  (let ((gh-vals (cdar gh-profile-alist)))
    (setf gh-vals (plist-put gh-vals :username "dieggsy")
          gh-vals (plist-put gh-vals :token (password-store-get "tokens/github/gist")))))
#+end_src
**** [[https://github.com/syohex/emacs-git-gutter-fringe][git-gutter-fringe]]
#+begin_quote
Fringe version of git-gutter.el
#+end_quote
#+begin_src emacs-lisp
(use-package git-gutter-fringe
  :defer 5
  :hook (focus-in . git-gutter:update-all-windows)
  :config
  (global-git-gutter-mode)
  (dolist (fringe '(git-gutter-fr:modified
                    git-gutter-fr:deleted
                    git-gutter-fr:added))
    (define-fringe-bitmap fringe
      (make-vector 29 #b00001111))))
#+end_src
**** [[https://github.com/magit/magit][magit]]
#+begin_quote
It's Magit! A Git Porcelain inside Emacs.
#+end_quote
Like git, for emacs. But cooler. (Just /trust/ me on this one.)
#+begin_src emacs-lisp
(use-package magit
  :straight
  :general
  (d/leader-keys
    "g"  '(:ignore t :wk "magit")
    "gB" 'd/magit-blame-toggle
    "gC" 'magit-clone
    "gL" 'magit-log-buffer-file
    "ga" 'magit-submodule-add
    "gb" 'magit-branch
    "gc" 'magit-checkout
    "gf" 'magit-find-file
    "gl" 'magit-log-all
    "gs" 'magit-status
    "gp" 'magit-file-popup)
  :config
  (setq magit-diff-refine-hunk t
        auto-revert-check-vc-info t
        git-commit-summary-max-length 50
        git-commit-major-mode 'org-mode)
  (d/hook! git-commit-mode
    (setq-local fill-column 72)
    (setq-local org-hide-emphasis-markers nil)
    (setq-local org-pretty-entities nil)))
#+end_src
**** [[https://github.com/alphapapa/magit-todos][magit-todos]]
#+begin_src emacs-lisp
(use-package magit-todos
  :after magit
  :demand t
  :config
  (magit-todos-mode))
#+end_src
**** [[https://github.com/vermiculus/magithub][magithub]]
#+begin_src emacs-lisp
(use-package magithub
  :after magit
  :demand t
  :general
  (nmap magit-status-mode-map
    "H" 'magithub-dispatch-popup)
  :config
  (magithub-feature-autoinject t))
#+end_src
* Tools
** [[elisp:(find-library-other-window%20"proced")][proced]]
#+begin_quote
operate on system processes like dired
#+end_quote
#+begin_src emacs-lisp
(use-package proced
  :straight nil
  :init
  (d/hook! proced-mode
    (proced-toggle-auto-update 1))
  :config
  (setq proced-auto-update-interval 2))
#+end_src
** [[elisp:(find-library-other-window "calc")][calc]]
#+begin_quote
the GNU Emacs calculator
#+end_quote
And it's amazing, to be quite honest. "The poor man's Mathematica", I once
heard (and can confirm).
#+begin_src emacs-lisp
(use-package calc
  :straight nil
  :general
  (d/leader-keys
    "ac" 'calc-dispatch)
  (emap calc-mode-map
    "x" (lambda () (interactive) (counsel-M-x "^calc-")))
  :init
  (add-hook 'calc-embedded-mode-hook #'d/calc-embedded-set-open-close)
  :config
  (evil-set-initial-state 'calc-mode 'emacs)
  (setq calc-multiplication-has-precedence nil)
  (setq calc-symbolic-mode t)
  (defun d/calc-embedded-set-open-close ()
    (when comment-start
      (setq-local calc-embedded-open-mode
                  (concat comment-start " ")))
    (when comment-end
      (setq-local calc-embedded-close-mode
                  (concat comment-end "\n")))))
#+end_src
** [[elisp:(find-library-other-window "eshell")][eshell]]
#+begin_quote
the Emacs command shell
#+end_quote
A shell written in emacs-lisp that has recently been replacing zsh for me.
*** Built-in
#+begin_src emacs-lisp
(use-package eshell
  :straight nil
  :commands d/eshell-floating
  :general
  (d/leader-keys
    "RET" 'd/eshell-here)
  :init
  (d/hook! eshell-mode
    (setq-local nobreak-char-display nil)
    (imap eshell-mode-map
      [remap eshell-pcomplete] 'completion-at-point
      "C-c C-p" (general-predicate-dispatch nil
                  (eshell-point-within-input-p (point)) 'eshell/ivy-ps)
      "C-c C-r" 'eshell/ivy-history
      "C-c C-l" 'd/eshell-clear-keystroke)
    (nmap eshell-mode-map
      "G" 'end-of-buffer))
  :config
  (d/hook! eshell-post-command
    (eshell-save-some-history))
  (setenv "PAGER" "cat")
  (setenv "GIT_PAGER" "cat")
  (setq eshell-buffer-maximum-lines 20000
        eshell-history-size 20000
        eshell-scroll-to-bottom-on-input 'this
        eshell-hist-ignoredups t
        eshell-destroy-buffer-when-process-dies t
        eshell-bad-command-tolerance 1.0e+INF
        eshell-list-files-after-cd t)
  (d/after 'em-term
    (d/list-prepend! 'eshell-visual-commands
      "htop" "cmus" "alsamixer" "nmtui" "nmtui-connect" "nmtui-edit"
      "ncdu"))
  :custom
  (eshell-preoutput-filter-functions '(xterm-color-filter))
  (eshell-term-name "eterm-256color")
  (eshell-cmpl-compare-entry-function 'string-lessp)
  (eshell-highlight-prompt nil)
  (eshell-prompt-function #'egp-theme)
  (eshell-confine-point-to-input nil)
  (eshell-output-filter-functions
   '(eshell-truncate-buffer
     eshell-postoutput-scroll-to-bottom
     eshell-handle-control-codes
     eshell-handle-ansi-color eshell-watch-for-password-prompt))
  (eshell-modules-list
   '(eshell-alias
     eshell-basic
     eshell-cmpl
     eshell-dirs
     eshell-glob
     eshell-hist
     eshell-ls
     eshell-pred
     eshell-prompt
     eshell-rebind
     eshell-script
     eshell-term
     eshell-tramp
     eshell-unix)))
#+end_src
*** Packages
**** [[elisp:(find-library-other-window%20"egp")][egp]]
#+begin_quote
Display git info in eshell
#+end_quote
#+begin_src emacs-lisp
(use-package egp
  :straight nil
  :commands egp-theme)
#+end_src
**** [[https://github.com/xuchunyang/eshell-z][eshell-z]]
#+begin_quote
cd to frequent directory in eshell, an Emacs port of https://github.com/rupa/z
#+end_quote
#+begin_src emacs-lisp
(use-package eshell-z
  :commands eshell/z)
#+end_src
**** [[https://github.com/Ambrevar/emacs-fish-completion][emacs-fish-completion]]
#+begin_quote
Fish completion for Emacs and Eshell
#+end_quote
#+begin_src emacs-lisp
(use-package fish-completion
  :hook (eshell-mode . fish-completion-mode))
#+end_src
**** [[https://github.com/leoliu/pcmpl-git-el][pcmpl-git]]
#+begin_quote
Emacs pcomplete for git
#+end_quote
#+begin_src emacs-lisp
(use-package pcmpl-git
  :after eshell
  :demand t)
#+end_src
**** [[https://github.com/dieggsy/company-eshell-autosuggest][company-eshell-autosuggest]]
#+begin_quote
Fish-like autosuggestions in eshell.
#+end_quote
#+begin_src emacs-lisp
(use-package esh-autosuggest
  :hook (eshell-mode . esh-autosuggest-mode)
  :straight (:host github
             :repo "dieggsy/esh-autosuggest")
  :general
  (esh-autosuggest-active-map
   "<S-return>" 'company-complete-selection))
#+end_src
**** [[https://github.com/dieggsy/eshell-thefuck][eshell-thefuck]]
#+begin_quote
Correct the previous eshell command.
#+end_quote
#+begin_src emacs-lisp
(use-package eshell-thefuck
  :straight (:host github
             :repo "dieggsy/eshell-thefuck"))
#+end_src
*** Functions
#+begin_src emacs-lisp
(d/after 'eshell
  (require 'f)
  (defun d/eshell-here ()
    "Opens up a new shell in the directory associated with the
current buffer's file. The eshell is renamed to match that
directory to make multiple eshell windows easier."
    (interactive)
    (let* ((height (/ (window-total-height) 3))
           (name (format "*eshell: %s*"
                         (f-filename default-directory))))
      (split-window-vertically (- height))
      (other-window 1)
      (let ((buf (get-buffer name)))
        (if buf
            (switch-to-buffer buf)
          (eshell 'new)
          (rename-buffer name 'unique)))
      (insert (concat "ls"))
      (eshell-send-input)))

  (defun eshell/clear ()
    "Custom `eshell' clear function to clear to top."
    (interactive)
    (let ((inhibit-read-only t))
      (erase-buffer)
      (delete-all-overlays)))

  (defun d/eshell-clear-keystroke ()
    "Allow for keystrokes to invoke eshell/clear."
    (interactive)
    (eshell/clear)
    (eshell-emit-prompt))

  (defun eshell/cpwd ()
    (kill-new (eshell/pwd)))

  (defun eshell/rg (&rest args)
    (eshell-grep "rg" (append '("--no-heading" "-M" "120") args) t))

  (defun eshell/zgrep (&rest args)
    (eshell-grep "zgrep" args t))

  (defun eshell/ivy-ps ()
    (interactive)
    (let ((ps (split-string
               (shell-command-to-string
                "ps axco user,pid,%cpu,%mem,start,time,command")
               "\n"
               t)))
      (ivy-read "[ps] "
                ps
                :action (lambda (x)
                          (insert (cadr (split-string x " " t)))))))

  (defun eshell/ivy-history ()
    (interactive)
    (let ((history
           (delete-dups
            (mapcar (lambda (str)
                      (string-trim (substring-no-properties str)))
                    (ring-elements eshell-history-ring))))
          (input (let* ((beg (save-excursion (eshell-bol)))
                        (end (save-excursion (end-of-line) (point))))
                   (buffer-substring-no-properties beg end))))
      (ivy-read "[history] "
                history
                :action (lambda (x)
                          (end-of-line)
                          (eshell-kill-input)
                          (insert x))
                :initial-input input)))

  (defvar d/eshell-floating-frame nil)
  (defun d/eshell-floating ()
    (interactive)
    (let ((frame-resize-pixelwise t))
      (unless (frame-live-p d/eshell-floating-frame)
        (setq d/eshell-floating-frame
              (make-frame `((name . "*eshell: floating*")
                            (display . ,(getenv "DISPLAY"))))))
      (let ((buf-name  "*eshell: floating*"))
        (with-selected-frame d/eshell-floating-frame
          (when (not (get-buffer buf-name))
            (with-current-buffer (eshell 'new)
              (setq mode-line-format nil)
              (rename-buffer buf-name)))
          (when (not
                 (member buf-name
                         (mapcar
                          (lambda (window)
                            (buffer-name (window-buffer window)))
                          (window-list))))
            (switch-to-buffer buf-name)))))))
#+end_src
** [[https://github.com/politza/pdf-tools][pdf-tools]]
#+begin_quote
Emacs support library for PDF files.
#+end_quote
#+begin_src emacs-lisp
(use-package pdf-tools
  :mode ("\\.pdf\\'" . pdf-tools-mode)
  :general
  (nmap pdf-view-mode-map
    "s" 'pdf-view-fit-width-to-window
    "a" 'pdf-view-fit-height-to-window
    "/" 'isearch-forward
    "J" 'pdf-view-next-page
    "K" 'pdf-view-previous-page
    "j" 'pdf-view-next-line-or-next-page
    "k" 'pdf-view-previou-line-or-previous-page
    "-" 'pdf-view-shrink
    "+" 'pdf-view-enlarg)
  :custom
  (pdf-view-midnight-colors '("#FDF4C1" . "#282828"))
  :config
  (pdf-tools-install))
#+end_src
** [[https://github.com/wasamasa/nov.el][nov.el]]
#+begin_quote
Major mode for reading EPUBs in Emacs
#+end_quote
#+begin_src emacs-lisp :tangle no
(use-package nov
  :mode ("\\.epub\\'" . nov-mode))
#+end_src
** [[elisp:(find-library-other-window "term")][term]]
#+begin_src emacs-lisp
(use-package term
  :general
  (d/leader-keys "at" 'd/ansi-term)
  :config
  (defun d/ansi-term ()
    (interactive)
    (ansi-term shell-file-name))

  (define-advice term-handle-exit (:after (&rest _) d/kill-buffer)
    (kill-buffer)))
#+end_src
* Emacs Enhancements
** Packages
*** [[https://www.emacswiki.org/emacs/centered-cursor-mode.el][centered-cursor-mode]]
#+begin_quote
Cursor stays vertically centered.
#+end_quote
I use this for reading, mostly.
#+begin_src emacs-lisp :tangle no
(use-package centered-cursor-mode)
#+end_src
*** [[https://github.com/bbatsov/crux][crux]]
#+begin_quote
A Collection of Ridiculously Useful eXtensions for Emacs
#+end_quote
#+begin_src emacs-lisp
(use-package crux
  :straight
  (crux :type git
        :host github
        :repo "bbatsov/crux"
        :no-autoloads t)
  :general
  (d/leader-keys
    "TAB" 'crux-switch-to-previous-buffer
    "fd" 'crux-delete-file-and-buffer
    "fr" 'crux-rename-file-and-buffer
    "bs" 'crux-sudo-edit)
  (d/mode-leader-keys emacs-lisp-mode-map
    "eR" 'crux-eval-and-replace)
  :config
  (crux-with-region-or-line eval-region)
  (crux-with-region-or-buffer indent-region)
  (crux-with-region-or-buffer untabify)
  (crux-with-region-or-buffer tabify)
  (crux-with-region-or-buffer fill-region))
#+end_src
*** [[https://github.com/purcell/disable-mouse][disable-mouse]]
#+begin_quote
Disable the mouse in Emacs
#+end_quote
#+begin_src emacs-lisp :tangle no
(use-package disable-mouse
  :defer 15
  :config
  (global-disable-mouse-mode)
  (dolist (key '([mouse-1]
                 [mouse-2]
                 [mouse-4]
                 [mouse-5]
                 [mouse-6]
                 [mouse-7]
                 [down-mouse-1]
                 [drag-mouse-1]
                 [wheel-right]
                 [double-wheel-right]
                 [triple-wheel-right]
                 [wheel-left]
                 [double-wheel-left]
                 [triple-wheel-left]
                 [wheel-down]
                 [double-wheel-down]
                 [triple-wheel-down]
                 [wheel-up]
                 [double-wheel-up]
                 [triple-wheel-up]))
    (define-key evil-motion-state-map key #'ignore)))
#+end_src
*** [[https://github.com/justbur/emacs-which-key][emacs-which-key]]
#+begin_quote
Emacs package that displays available keybindings in popup
#+end_quote
#+begin_src emacs-lisp
(use-package which-key
  :defer 20
  :general
  (d/leader-keys
    "hk" 'which-key-show-top-level)
  :config
  (which-key-mode)

  (defmacro d/declare-prefix (&rest body)
    (declare (indent defun))
    `(which-key-add-key-based-replacements
       ,@(cl-loop
          for (prefix name) on body
          by #'cddr
          while name
          append `(,(concat "SPC " prefix) ,name
                   ,(concat "C-SPC " prefix) ,name))))

  (defmacro d/declare-mode-prefix (modes &rest body)
    (declare (indent defun))
    (let ((modes (if (listp modes) modes (list modes))))
      `(progn
         ,@(cl-loop
            for mode in modes collect
            `(which-key-add-major-mode-key-based-replacements ',mode
               ,@(cl-loop
                  for (prefix name) on body
                  by #'cddr
                  while name
                  append `(,(concat ", " prefix) ,name
                           ,(concat "C-, " prefix) ,name)))))))

  (defmacro d/which-key-remove-prefix (&rest body)
    (declare (indent defun))
    `(progn
       ,@(cl-loop for regexp in body collect
                  `(push '((nil . ,(concat regexp "\\(.+\\)")) . (nil . "\\1"))
                         which-key-replacement-alist))))


  (d/declare-prefix
    "a"   "applications"
    "b"   "buffer"
    "f"   "file"
    "h"   "help"
    "hd"  "describe"
    "i"   "insert"
    "j"   "jump"
    "n"   "narrow/numbers"
    "q"   "quit"
    "s"   "search"
    "w"   "window"
    "xi"  "indent"
    "xl"  "lines"
    "SPC" "root")

  (d/declare-mode-prefix emacs-lisp-mode
    "e" "eval"
    "p" "pp")

  (setq which-key-sort-order 'which-key-key-order-alpha)
  (setq which-key-sort-uppercase-first nil)

  (d/which-key-remove-prefix
    "avy-"
    "counsel-"
    "counsel-projectile-"
    "crux-"
    "customize-"
    "d/"
    "evil-mc-"
    "evilnc-"
    "eyebrowse-"
    "eyebrowse-switch-to-"
    "ivy-"
    "magit-"
    "projectile-"))
#+end_src
#+begin_quote
Package to display keyboard macros or latest interactive commands as emacs lisp.
#+end_quote
*** [[https://github.com/jschaf/esup][esup]]
#+begin_quote
ESUP - Emacs Start Up Profiler
#+end_quote
#+begin_src emacs-lisp
(use-package esup
  :config
  (setq esup-insignificant-time 0.001))
#+end_src
*** [[https://github.com/lewang/flx][flx]]
#+begin_quote
Fuzzy matching for Emacs ... a la Sublime Text.
#+end_quote
#+begin_src emacs-lisp
(use-package flx)
#+end_src
*** [[https://git.zx2c4.com/password-store/tree/contrib/emacs][password-store]]
#+begin_quote
Password store (pass) support
#+end_quote
#+begin_src emacs-lisp
(use-package password-store
  :config
  (setq password-store-password-length 20))
#+end_src
*** [[https://github.com/Fanael/persistent-scratch][persistent-scratch]]
#+begin_quote
Preserve the scratch buffer across Emacs sessions
#+end_quote
#+begin_src emacs-lisp
(use-package persistent-scratch
  :defer 10
  :config
  (with-current-buffer "*scratch*"
    (emacs-lisp-mode))
  (persistent-scratch-setup-default))
#+end_src
*** [[https://github.com/iqbalansari/restart-emacs][restart-emacs]]
#+begin_quote
A simple emacs package to restart emacs from within emacs.
#+end_quote
SUPER nifty.
#+begin_src emacs-lisp
(use-package restart-emacs
  :general (d/leader-keys
             ;; "qr" 'restart-emacs
             "qr" (lambda ()
                    (interactive)
                    (message "Use ':qr' to restart emacs.")))
  :init
  (evil-ex-define-cmd "qr[estart]" 'restart-emacs))
#+end_src
*** [[https://github.com/nonsequitur/smex][smex]]
#+begin_quote
A smart M-x enhancement for Emacs.
#+end_quote
Sorts ivy by most recently used, I think.
#+begin_src emacs-lisp
(use-package smex)
#+end_src
* Fun
** Packages
*** [[https://github.com/josuah/drawille][drawille]]
#+begin_quote
Drawille library implementation in elisp.
#+end_quote
Draws stuff in ascii.
#+begin_src emacs-lisp :tangle no
(use-package drawille)
#+end_src
*** [[https://www.gnu.org/software/emms/][emms]]
#+begin_quote
The Emacs Multimedia System. Plays multimedia files from Emacs using a variety
of external players.
#+end_quote
#+begin_src emacs-lisp :tangle no
(use-package emms
  :commands emms-smart-browse
  :general
  (d/leader-keys
    "m" 'emms/body)
  :custom
  (emms-show-format "%s")
  :config
  (require 'emms-setup)
  (emms-all)
  (setq emms-source-file-default-directory "~/Music/"
        emms-player-mpd-music-directory "~/Music/"
        emms-seek-seconds 5)
  (require 'emms-player-mpd)
  (add-to-list 'emms-player-list 'emms-player-mpd)
  (emms-player-mpd-connect)
  (defhydra emms ()
    "
%s(emms-track-get (emms-playlist-current-selected-track) 'info-title)
%s(emms-track-get (emms-playlist-current-selected-track) 'info-artist) - %s(emms-track-get (emms-playlist-current-selected-track) 'info-album)
        🔀
  _z_   _c_   _b_    _s_
"
    ("c" emms-pause)
    ("b" emms-next)
    ("z" emms-previous)
    ("s" emms-shuffle)
    ("q" nil)))
#+end_src
*** [[https://github.com/johanvts/emacs-fireplace/][fireplace]]
#+begin_quote
A cozy fireplace for emacs.
#+end_quote
For the cold winters.
#+begin_src emacs-lisp :tangle no
(use-package fireplace
  :general
  (nmap fireplace-mode-map
    "q" 'fireplace-off
    "Q" 'fireplace-off
    "-" 'fireplace-down
    "=" 'fireplace-up
    "*" 'fireplace-toggle-smoke))
#+end_src
*** [[https://github.com/dieggsy/emacs-hacker-typer][hacker-typer]]
#+begin_quote
A customizable implementation of http://hackertyper.com in emacs.
#+end_quote
#+begin_src emacs-lisp :tangle no
(use-package hacker-typer
  :config
  (setq hacker-typer-show-hackerman t
        hacker-typer-remove-comments t))
#+end_src
*** [[https://www.emacswiki.org/emacs/highlight-tail.el][highlight-tail]]
#+begin_quote
Draw a colourful "tail" while you write
#+end_quote
(pure awesome)
#+begin_src emacs-lisp :tangle no
(use-package highlight-tail)
#+end_src
*** [[https://github.com/rbanffy/selectric-mode][selectric-mode]]
#+begin_quote
Make your Emacs sound like a proper typewriter.
#+end_quote
Clackity-clack.
#+begin_src emacs-lisp :tangle no
(use-package selectric-mode)
#+end_src
*** [[https://github.com/parkouss/speed-type][speed-type]]
#+begin_quote
Practice touch/speed typing in emacs.
#+end_quote
#+begin_src emacs-lisp
(use-package speed-type
  :general (d/leader-keys "as" 'speed-type-text))
#+end_src
*** [[https://gitlab.com/iankelling/spray][spray]]
#+begin_quote
A speed reading mode for Emacs.
#+end_quote
#+begin_src emacs-lisp :tangle no
(use-package spray)
#+end_src
*** [[https://github.com/dieggsy/turing-machine][turing-machine]]
#+begin_quote
Single-tape Turing machine simulator
#+end_quote
#+begin_src emacs-lisp :tangle no
(use-package turing-machine)
#+end_src
*** [[https://github.com/vibhavp/emacs-xkcd][xkcd]]
#+begin_quote
Read xkcd from Emacs.
#+end_quote
#+begin_src emacs-lisp :tangle no
(use-package xkcd
  :general
  (d/leader-keys "ax" 'xkcd)
  (nmap xkcd-mode-map
    "j" 'xkcd-next
    "h" 'xkcd-prev
    "k" 'xkcd-prev
    "l" 'xkcd-next
    "t" 'xkcd-alt-text
    "q" 'xkcd-kill-buffer
    "c" 'xkcd-copy-link
    "g" 'xkcd-get
    "r" 'xkcd-rand
    "o" 'xkcd-open-browser
    "e" 'xkcd-open-explanation-browser
    "G" 'xkcd-get-latest))
#+end_src
** Functions
#+begin_src emacs-lisp
(eval-when-compile
  (defvar zone-programs))

(defun d/zone-choose ()
  "Choose a PGM to run for `zone'.

Source: http://tinyurl.com/lo96nwc"
  (interactive)
  (require 'zone nil t)
  (let* ((pgm (completing-read
               "Program: "
               (mapcar #'symbol-name zone-programs)))
         (zone-programs (list (intern pgm))))
    (redisplay)
    (zone)))
#+end_src
** Bindings
#+begin_src emacs-lisp
(d/leader-keys
  "ag"  '(:ignore t :wk "games")
  "agd" 'dunnet
  "agg" 'gomoku
  "agt" 'tetris)
#+end_src
* Web services
** Built-in
*** [[elisp:(find-library-other-window "erc")][ERC]]
#+begin_quote
An Emacs Internet Relay Chat client
#+end_quote
**** Defaults
#+begin_src emacs-lisp
(d/after 'erc
  (setq erc-prompt (format "\n%s>" (make-string 12 ?\s))
        erc-fill-prefix (format "%s\t" (make-string 10 ?\s))
        erc-notice-prefix   (format "%s\t***\s" (make-string 10 ?\s))
        erc-format-nick-function 'd/erc-format-nick
        erc-nick "dieggsy"
        erc-prompt-for-password nil
        erc-input-line-position -1
        erc-autojoin-timing 'ident
        erc-header-line-format nil
        erc-fill-column 79
        erc-hide-list '("353")
        erc-lurker-threshold-time (* 6 60 60)
        erc-lurker-hide-list '("JOIN" "PART" "QUIT" "NICK")
        erc-kill-buffer-on-part t
        erc-kill-queries-on-quit t
        erc-kill-server-buffer-on-quit t
        erc-rename-buffers t
        erc-join-buffer 'bury
        erc-track-use-faces nil
        erc-track-priority-faces-only 'all
        erc-track-exclude-server-buffer t
        erc-format-query-as-channel-p t
        erc-log-channels-directory (no-littering-expand-var-file-name "erc/logs")
        erc-generate-log-file-name-function 'erc-generate-log-file-name-short
        erc-track-faces-priority-list '(erc-error-face
                                        erc-current-nick-face
                                        erc-keyword-face
                                        erc-nick-msg-face
                                        erc-direct-msg-face
                                        erc-dangerous-host-face)
        erc-autojoin-channels-alist '(("freenode.net"
                                       "#emacs"
                                       "##linux"
                                       "#archlinux"
                                       "#lisp"
                                       "##lisp"
                                       "#scheme"
                                       "#chicken")))

  (erc-define-catalog-entry 'english 'ACTION (format "%s*\t%%n %%a" (make-string 9 ?\s))))
#+end_src
**** Built-in
#+begin_src emacs-lisp
(use-package erc
  :straight nil
  :commands d/erc-floating
  :general
  (nmap erc-mode-map
    "G" (lambda ()
          (interactive)
          (evil-goto-line) (erc-scroll-to-bottom)))
  (d/mode-leader-keys erc-mode-map
    "g" 'd/erc-switch-to-buffer)
  (d/leader-keys
    "ai" 'd/erc-floating)
  (imap erc-mode-map
    [up] 'erc-previous-command
    [down] 'erc-next-command)
  :config
  (add-hook 'window-configuration-change-hook #'d/erc-to-bottom)
  (add-hook 'erc-insert-post-hook #'erc-save-buffer-in-logs)
  (add-hook 'erc-send-pre-hook 'd/erc-preprocess)
  :custom
  (erc-modules
   '(;; log
     spelling
     notifications
     pcomplete
     netsplit
     ;; fill
     button
     track
     stamp
     readonly
     networks
     ring
     autojoin
     noncommands
     irccontrols
     move-to-prompt
     menu
     match)))
#+end_src
**** Variables
#+begin_src emacs-lisp
(d/after 'erc
  (defvar d/erc-last-speaker (make-hash-table :test 'equal))
  (setq erc-lurker-state (make-hash-table :test 'equal)))
#+end_src
**** Functions
#+begin_src emacs-lisp
(d/after 'erc
  ;; This function messes with our own buttons
  (advice-add #'erc-button-remove-old-buttons :override #'ignore)

  (defmacro d/erc-nick-format-function-body (cond nick &optional else)
    "If COND is true, format NICK as specified below, otherwise return ELSE.

NICK will be truncated if it is longer than 10 chars and '…' will
be appended to the end. Nick is propertized using either
`erc-current-nick-face' or `erc-hl-nicks-make-face'. If NICK is
the last speaker in the channel, '→' will be displayed instead.
This also updates lurker information and adds buttons (which I
think is necessary since we 're so heavily messing with ERC
internals).

Basically, this kind of makes ERC act like weechat."
    `(if ,cond
         (let* ((nick ,nick)
                (trimmed (erc-hl-nicks-trim-irc-nick nick))
                (server (erc-canonicalize-server-name
                         erc-server-announced-name)))
           (unless (gethash server d/erc-last-speaker)
             (puthash server (make-hash-table :test 'equal) d/erc-last-speaker))
           (let* ((channel (buffer-name))
                  (mode (erc-get-user-mode-prefix nick))
                  (nick-was-last (string=
                                  (gethash channel
                                           (gethash server d/erc-last-speaker))
                                  nick))
                  (long-char
                   (if (and (> (length nick) 10)
                            (not nick-was-last))
                       (propertize "…" 'face '(:foreground "#D3869B" ))
                     "")))

             (when (>= (cl-incf erc-lurker-cleanup-count)
                       erc-lurker-cleanup-interval)
               (setq erc-lurker-cleanup-count 0)
               (erc-lurker-cleanup))
             (unless (gethash server erc-lurker-state)
               (puthash server (make-hash-table :test 'equal) erc-lurker-state))
             (puthash trimmed (current-time)
                      (gethash server erc-lurker-state))
             (puthash channel nick (gethash server d/erc-last-speaker))
             (format "%10.10s%s\t"
                     (concat
                      (when (not nick-was-last)
                        (propertize mode 'face 'erc-nick-prefix-face
                                    'mouse-face 'font-lock-keyword-face))
                      (propertize
                       (if nick-was-last "→" nick)
                       'face
                       (cond ((string= nick (erc-current-nick))
                              'erc-current-nick-face)
                             ((not (member trimmed erc-hl-nicks-skip-nicks))
                              (erc-hl-nicks-make-face trimmed))
                             (t nil))
                       'mouse-face erc-button-mouse-face
                       'keymap erc-button-keymap
                       'erc-callback #'erc-nick-popup
                       'erc-data (list nick)))
                     long-char)))
       ,else))

  (defun d/erc-format-nick (&optional user channel-data)
    "Truncate nick when too long, substitute when repeated speaker, and
update lurker status."
    (d/erc-nick-format-function-body
     user
     (erc-server-user-nickname user)))

  (define-advice erc-format-my-nick (:override nil truncate-substitute-lurk)
    "Truncate nick when too long, substitute when repeated speaker, and
update lurker status."
    (d/erc-nick-format-function-body
     erc-show-my-nick
     (erc-current-nick)
     (let ((prefix (format "%10.10s\t" ">")))
       (propertize prefix 'font-lock-face 'erc-current-nick-face))))

  (define-advice erc-format-privmessage
      (:override (nick msg privp msgp) no-brackets)
    "Remove the annoying angle brackets."
    (let* ((mark-s (if msgp (if privp "*" "") (format "%s\t" (make-string 10 ?\s))))
           (mark-e (if msgp (if privp "*" "") ": "))
           (str    (format "%s%s%s%s" mark-s nick mark-e msg))
           (nick-face (if privp 'erc-nick-msg-face 'erc-nick-default-face))
           (msg-face (if privp 'erc-direct-msg-face 'erc-default-face)))
      ;; add text properties to text before the nick, nick and after nick
      (erc-put-text-property 0 (length mark-s) 'font-lock-face msg-face str)
      (erc-put-text-property (length mark-s) (+ (length mark-s) (length nick))
                             'font-lock-face nick-face str)
      (erc-put-text-property (+ (length mark-s) (length nick)) (length str)
                             'font-lock-face msg-face str)
      str))

  (define-advice erc-track-find-face (:around (fn faces) promote-query)
    "Promote query buffers as if everything contains current nick.

Source: http://tinyurl.com/y8tj8vxx"
    (if (erc-query-buffer-p)
        (intern "erc-current-nick-face")
      (funcall fn faces)))

  (define-advice erc-track-modified-channels (:around (fn) promote-query)
    "Promote query buffers as if everything contains current nick when
only tracking priority faces.

Source: http://tinyurl.com/y8tj8vxx"
    (when (erc-query-buffer-p) (setq erc-track-priority-faces-only nil))
    (funcall fn)
    (when (erc-query-buffer-p) (setq erc-track-priority-faces-only 'all)))

  (define-advice erc-notifications-notify (:override (nick msg) channel-title)
    "Use channel as notification title and remove erc-fill-prefix."
    (let ((server (erc-canonicalize-server-name
                   erc-server-announced-name))
          (channel (buffer-name))
          (msg (string-join
                (split-string
                 (replace-regexp-in-string erc-fill-prefix "" msg)
                 nil
                 t
                 " ")
                " ")))
      (dbus-ignore-errors
        (setq erc-notifications-last-notification
              (notifications-notify
               :bus erc-notifications-bus
               :title (xml-escape-string (format "%s@%s" channel server))
               :body (xml-escape-string (format "%s: %s" nick msg))
               :replaces-id erc-notifications-last-notification
               :app-icon erc-notifications-icon)))))

  (defun d/erc-to-bottom ()
    (and (eq major-mode 'erc-mode)
         (erc-scroll-to-bottom)))

  (defun d/erc-switch-to-buffer (&optional arg)
    (interactive "P")
    (let ((ivy-use-virtual-buffers nil))
      (erc-switch-to-buffer arg)))

  (defvar d/erc-floating-frame nil)
  (defun d/erc-floating ()
    (interactive)
    (let ((frame-resize-pixelwise t))
      (unless (frame-live-p d/erc-floating-frame)
        (setq d/erc-floating-frame
              (make-frame `((name . "*erc: floating*")
                            (left-fringe . 0)
                            (right-fringe . 0)
                            (unsplittable . t)
                            (display . ,(getenv "DISPLAY"))))))
      (with-selected-frame d/erc-floating-frame
        ;; we do this cause emacsclient starts out with the server buffer
        (switch-to-buffer (window-buffer (selected-window)))
        ;; If buffer has a live ERC process, do nothing
        (unless (erc-server-buffer-live-p)
          ;; Otherwise, find the first ERC process
          (let ((live-erc-proc
                 (cl-find-if (lambda (proc)
                               (string-match-p "erc" (process-name proc)))
                             (cl-remove-if-not
                              #'process-live-p
                              (process-list)))))
            ;; If we have one, switch to it
            (if live-erc-proc
                (progn
                  (switch-to-buffer (process-buffer live-erc-proc)))
              ;; otherwise, connect to ERC
              (switch-to-buffer (erc-tls :server "firefly"
                                         :port 4000))))))))

  (defun d/dogbike ()
    (interactive)
    (let ((erc-server-connect-function 'erc-open-tls-stream))
      (erc :server "firefly"
           :port 4000
           :nick "dieggsy/dogbike")))

  (defun d/erc-preprocess (string)
    (setq str
          (string-trim
           (replace-regexp-in-string "\n+" " " str)))))
#+end_src
**** Setup
#+begin_src emacs-lisp
(d/hook! erc-mode
  (visual-line-mode)
  (setq wrap-prefix (make-string 14 ?\s))
  (set-display-table-slot standard-display-table 'wrap ?\s)
  (setq tab-width 7)
  (setq mode-line-format nil))
#+end_src
** Packages
*** [[https://github.com/leathekd/erc-hl-nicks][erc-hl-nicks]]
#+begin_quote
Nickname Highlighting for ERC
#+end_quote
#+begin_src emacs-lisp
(use-package erc-hl-nicks
  :straight (:host github
             :repo "dieggsy/erc-hl-nicks"
             :fork (:host github
                    :repo "leathekd/erc-hl-nicks"))
  :after erc
  :demand t
  :config
  (setq erc-hl-nicks-skip-nicks '("diegs" "dieggsy")
        erc-lurker-ignore-chars erc-hl-nicks-ignore-chars)
  (push "erc-current-nick-face" erc-hl-nicks-skip-faces))
#+end_src
*** [[https://github.com/skeeto/elfeed][elfeed]]
#+begin_quote
An Emacs web feeds client
#+end_quote
Configure the Elfeed RSS reader with an Orgmode file
**** Package
#+begin_src emacs-lisp
(use-package elfeed
  :general
  (d/leader-keys "ae" 'elfeed))
#+end_src
**** Enhancements
***** [[https://github.com/remyhonig/elfeed-org][elfeed-org]]
#+begin_quote
Configure the Elfeed RSS reader with an Orgmode file
#+end_quote
#+begin_src emacs-lisp
(use-package elfeed-org
  :after elfeed
  :demand t
  :config
  (elfeed-org))
#+end_src
***** [[https://github.com/algernon/elfeed-goodies][elfeed-goodies]]
#+begin_quote
Various goodies for Elfeed
#+end_quote
#+begin_src emacs-lisp
(use-package elfeed-goodies
  :after elfeed
  :demand t
  :config
  (elfeed-goodies/setup)
  (setq elfeed-goodies/entry-pane-position 'bottom))
#+end_src
*** [[https://github.com/aaronbieber/sunshine.el][sunshine]]
#+begin_quote
An Emacs package for displaying the forecast from OpenWeatherMap.
#+end_quote
#+begin_src emacs-lisp :tangle no
(use-package sunshine
  :general
  (d/leader-keys
    "aW" 'sunshine-quick-forecast
    "aw" 'sunshine-forecast)
  :config
  (setq sunshine-location "02139,USA"
        sunshine-appid (password-store-get "api/openweathermap")))
#+end_src
* Local vars
#+begin_example
# Local Variables:
# eval: (add-hook 'after-save-hook 'd/async-babel-tangle 'append 'local)
# End:
#+end_example
